ВЫБРАТЬ
	
	СправочникНоменклатура.Ссылка КАК Ссылка,
	СправочникНоменклатура.Артикул КАК Артикул,
	СправочникНоменклатура.Код КАК Код,
	СправочникНоменклатура.Наименование КАК Наименование,
	СправочникНоменклатура.ПометкаУдаления КАК ПометкаУдаления,
	СправочникНоменклатура.Родитель КАК Родитель,
	СправочникНоменклатура.ЭтоГруппа КАК ЭтоГруппа,
	СправочникНоменклатура.Ссылка КАК Номенклатура,
	СправочникНоменклатура.Ссылка КАК ЭлементСписка,
	СправочникНоменклатура.ВидНоменклатуры КАК ВидНоменклатуры,
	СправочникНоменклатура.ТипНоменклатуры КАК ТипНоменклатуры,
	
	ВЫБОР КОГДА СправочникНоменклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Набор) ТОГДА
		ЕСТЬNULL(ВариантыКомплектацииНоменклатуры.ВариантРасчетаЦеныНабора, ЗНАЧЕНИЕ(Перечисление.ВариантыРасчетаЦенНаборов.ПустаяСсылка))
	ИНАЧЕ
		ЗНАЧЕНИЕ(Перечисление.ВариантыРасчетаЦенНаборов.ПустаяСсылка)
	КОНЕЦ КАК ВариантРасчетаЦеныНабора,
	ВариантыКомплектацииНоменклатуры.СодержитТовары КАК СодержитТовары,
	ВариантыКомплектацииНоменклатуры.СодержитУслуги КАК СодержитУслуги,
	
	СправочникНоменклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	СправочникНоменклатура.ЦеноваяГруппа КАК ЦеноваяГруппа,
	СправочникНоменклатура.Качество КАК Качество,
	СправочникНоменклатура.ИспользованиеХарактеристик КАК ИспользованиеХарактеристик,
	ВЫБОР
		КОГДА СправочникНоменклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар), ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
			ТОГДА ИСТИНА
		КОГДА СправочникНоменклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Набор)) И ЕСТЬNULL(ВариантыКомплектацииНоменклатуры.СодержитТовары, ЛОЖЬ) И Не ЕСТЬNULL(ВариантыКомплектацииНоменклатуры.СодержитУслуги, ЛОЖЬ)
			ТОГДА ИСТИНА
		ИНАЧЕ ЛОЖЬ
	КОНЕЦ КАК ЭтоТовар,
	ВЫБОР
		КОГДА СправочникНоменклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга), ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа))
			ТОГДА ИСТИНА
		КОГДА СправочникНоменклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Набор)) И ЕСТЬNULL(ВариантыКомплектацииНоменклатуры.СодержитУслуги, ЛОЖЬ) И НЕ ЕСТЬNULL(ВариантыКомплектацииНоменклатуры.СодержитТовары, ЛОЖЬ)
			ТОГДА ИСТИНА
		ИНАЧЕ ЛОЖЬ
	КОНЕЦ КАК ЭтоУслуга,
	ВЫБОР
		КОГДА СправочникНоменклатура.ИспользованиеХарактеристик = ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.ОбщиеДляВидаНоменклатуры)
			ТОГДА СправочникНоменклатура.ВидНоменклатуры
		КОГДА СправочникНоменклатура.ИспользованиеХарактеристик = ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.ИндивидуальныеДляНоменклатуры)
			ТОГДА СправочникНоменклатура.Ссылка
		КОГДА СправочникНоменклатура.ИспользованиеХарактеристик = ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.ОбщиеСДругимВидомНоменклатуры)
			ТОГДА СправочникНоменклатура.ВладелецХарактеристик
		ИНАЧЕ Неопределено
	КОНЕЦ КАК ВладелецХарактеристик,
	ВЫБОР
		КОГДА СправочникНоменклатура.ИспользованиеХарактеристик = ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.НеИспользовать)
			ТОГДА ЛОЖЬ
		ИНАЧЕ ИСТИНА
	КОНЕЦ КАК ХарактеристикиИспользуются,
	ВЫБОР
		КОГДА СправочникНоменклатура.ЭтоГруппа
			ТОГДА ВЫБОР
					КОГДА СправочникНоменклатура.ПометкаУдаления
						ТОГДА 13
					ИНАЧЕ 12
				КОНЕЦ
		ИНАЧЕ ВЫБОР
				КОГДА СправочникНоменклатура.ЕстьТоварыДругогоКачества
					ТОГДА 4 + ВЫБОР
							КОГДА СправочникНоменклатура.ПометкаУдаления
								ТОГДА 1
							ИНАЧЕ 0
						КОНЕЦ + ВЫБОР
							КОГДА СправочникНоменклатура.ИспользованиеХарактеристик = ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.НеИспользовать)
								ТОГДА 0
							ИНАЧЕ 2
						КОНЕЦ
				ИНАЧЕ -1 + ВЫБОР
						КОГДА СправочникНоменклатура.ПометкаУдаления
							ТОГДА 1
						ИНАЧЕ 0
					КОНЕЦ + ВЫБОР
						КОГДА СправочникНоменклатура.ИспользованиеХарактеристик = ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.НеИспользовать)
							ТОГДА 1
						ИНАЧЕ 3
					КОНЕЦ
			КОНЕЦ
	КОНЕЦ КАК ИндексКартинки,

	ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка) КАК Упаковка,
	0 КАК Цена,
	ЗНАЧЕНИЕ(Справочник.ВидыЦен.ПустаяСсылка) КАК ВидЦены,
	ВЫРАЗИТЬ(ЕСТЬNULL(ВсеОстатки.ВНаличии, 0) КАК ЧИСЛО(15, 3)) КАК ВНаличииОстаток,
	ЕСТЬNULL(ВсеОстатки.ВНаличии, 0) КАК ВНаличииВБазовыхЕдиницах,
	ВЫРАЗИТЬ(ЕСТЬNULL(ВсеОстатки.Свободно, 0) КАК ЧИСЛО(15, 3)) КАК Доступно
ИЗ
	
	Справочник.Номенклатура КАК СправочникНоменклатура
		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВариантыКомплектацииНоменклатуры КАК ВариантыКомплектацииНоменклатуры
		ПО ВариантыКомплектацииНоменклатуры.Владелец = СправочникНоменклатура.Ссылка
		И ВариантыКомплектацииНоменклатуры.Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
		И ВариантыКомплектацииНоменклатуры.Основной

	
	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаспределениеЗапасов КАК ВсеОстатки
		ПО ВсеОстатки.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОстатокНаСкладе)
		И ВсеОстатки.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		И ВсеОстатки.Склад = &Склад
		И ВсеОстатки.Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
		И ВсеОстатки.Номенклатура = СправочникНоменклатура.Ссылка

ГДЕ
	
	НЕ СправочникНоменклатура.ЭтоГруппа

	
{ГДЕ
	(1 В
		(ВЫБРАТЬ ПЕРВЫЕ 1
			1
		ИЗ
			РегистрСведений.НоменклатураСегмента КАК Сегменты
		ГДЕ
			Сегменты.Номенклатура = СправочникНоменклатура.Ссылка
			И Сегменты.Сегмент = &СегментНоменклатуры))}

