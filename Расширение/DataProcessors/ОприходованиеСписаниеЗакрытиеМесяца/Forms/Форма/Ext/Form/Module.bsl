&НаКлиенте
Процедура СледующийМесяц(Команда)
	ДатаЗакрытия = Дата(?(Месяц(ДатаЗакрытия) = 12, Год(ДатаЗакрытия) + 1, Год(ДатаЗакрытия)), ?(Месяц(ДатаЗакрытия) = 12, 1, Месяц(ДатаЗакрытия) + 1), 1);
	ДатаЗакрытияПриИзменении(Неопределено);
КонецПроцедуры

&НаКлиенте
Процедура ДатаЗакрытияПриИзменении(Элемент)
	ДатаЗакрытия = НачалоМесяца(ДатаЗакрытия);
	ТабДанные.Очистить();
КонецПроцедуры

&НаСервере
Процедура СоздатьОприходованияНаСервере()
	
	Запрос1 = Новый Запрос("
		|ВЫБРАТЬ
		|	ТоварыОрганизацийОстатки.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
		|	ТоварыОрганизацийОстатки.Организация КАК Организация,
		|	ТоварыОрганизацийОстатки.ВидЗапасов КАК ВидЗапасов,
		|	ТоварыОрганизацийОстатки.НомерГТД КАК НомерГТД,
		|	ТоварыОрганизацийОстатки.КоличествоОстаток КАК КоличествоОстаток,
		|	ТоварыОрганизацийОстатки.КоличествоПоРНПТОстаток КАК КоличествоПоРНПТОстаток,
		|	ТоварыОрганизацийОстатки.АналитикаУчетаНоменклатуры.Номенклатура КАК АналитикаУчетаНоменклатурыНоменклатура,
		|	ТоварыОрганизацийОстатки.АналитикаУчетаНоменклатуры.Характеристика КАК АналитикаУчетаНоменклатурыХарактеристика,
		|	ТоварыОрганизацийОстатки.АналитикаУчетаНоменклатуры.СкладскаяТерритория КАК АналитикаУчетаНоменклатурыСкладскаяТерритория
		|ПОМЕСТИТЬ ПодИтог
		|ИЗ
		|	РегистрНакопления.ТоварыОрганизаций.Остатки(&ПериодГраница, ) КАК ТоварыОрганизацийОстатки
		|ГДЕ
		|	ТоварыОрганизацийОстатки.КоличествоОстаток < 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МАКСИМУМ(ПриобретениеТоваровУслугТовары.Ссылка.Дата) КАК Дата,
		|	ПриобретениеТоваровУслугТовары.Номенклатура КАК Номенклатура,
		|	ПриобретениеТоваровУслугТовары.Характеристика КАК Характеристика
		|ПОМЕСТИТЬ МаксДата
		|ИЗ
		|	Документ.ПриобретениеТоваровУслуг.Товары КАК ПриобретениеТоваровУслугТовары
		|ГДЕ
		|	ПриобретениеТоваровУслугТовары.Ссылка.Дата <= &Период
		|	И ПриобретениеТоваровУслугТовары.Ссылка.Проведен
		|	И (ПриобретениеТоваровУслугТовары.Номенклатура, ПриобретениеТоваровУслугТовары.Характеристика) В
		|			(ВЫБРАТЬ
		|				ПодИтог.АналитикаУчетаНоменклатурыНоменклатура,
		|				ПодИтог.АналитикаУчетаНоменклатурыХарактеристика
		|			ИЗ
		|				ПодИтог КАК ПодИтог)
		|
		|СГРУППИРОВАТЬ ПО
		|	ПриобретениеТоваровУслугТовары.Номенклатура,
		|	ПриобретениеТоваровУслугТовары.Характеристика
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МаксДата.Номенклатура КАК Номенклатура,
		|	МаксДата.Характеристика КАК Характеристика,
		|	МАКСИМУМ(ПриобретениеТоваровУслугТовары.Цена) КАК Цена
		|ПОМЕСТИТЬ ЦеныНоменклатуры
		|ИЗ
		|	МаксДата КАК МаксДата
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПриобретениеТоваровУслуг.Товары КАК ПриобретениеТоваровУслугТовары
		|		ПО МаксДата.Номенклатура = ПриобретениеТоваровУслугТовары.Номенклатура
		|			И МаксДата.Характеристика = ПриобретениеТоваровУслугТовары.Характеристика
		|			И МаксДата.Дата = ПриобретениеТоваровУслугТовары.Ссылка.Дата
		|
		|СГРУППИРОВАТЬ ПО
		|	МаксДата.Номенклатура,
		|	МаксДата.Характеристика
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПодИтог.АналитикаУчетаНоменклатурыНоменклатура КАК Номенклатура,
		|	ПодИтог.АналитикаУчетаНоменклатурыХарактеристика КАК Характеристика,
		|	ПодИтог.Организация КАК Организация,
		|	ПодИтог.АналитикаУчетаНоменклатурыСкладскаяТерритория КАК Склад,
		|	-ПодИтог.КоличествоОстаток КАК Количество,
		|	ПодИтог.НомерГТД КАК НомерГТД,
		|	ЦеныНоменклатуры.Цена КАК Цена
		|ИЗ
		|	ПодИтог КАК ПодИтог
		|		ЛЕВОЕ СОЕДИНЕНИЕ ЦеныНоменклатуры КАК ЦеныНоменклатуры
		|		ПО (ПодИтог.АналитикаУчетаНоменклатурыНоменклатура = ЦеныНоменклатуры.Номенклатура)
		|			И (ПодИтог.АналитикаУчетаНоменклатурыХарактеристика = ЦеныНоменклатуры.Характеристика)
		|ИТОГИ ПО
		|	Организация,
		|	Склад");

	Запрос1.УстановитьПараметр("Период", КонецМесяца(ДатаЗакрытия));
	Запрос1.УстановитьПараметр("ПериодГраница", Новый Граница(КонецМесяца(ДатаЗакрытия), ВидГраницы.Включая));

	РезультатЗапроса = Запрос1.Выполнить();
	
	ВыборкаОрганизация = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ТабДанные.Очистить();
	
	СтатьяДоходов = ПланыВидовХарактеристик.СтатьиДоходов.ВыручкаОтПродаж;
	Валюта = Справочники.Валюты.ПолучитьСсылку(Новый УникальныйИдентификатор("d8e87a89-76e3-11ec-810f-005056ad28ac")); //643 RUB
	
	Пока ВыборкаОрганизация.Следующий() Цикл
		
		ВыборкаСклад = ВыборкаОрганизация.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаСклад.Следующий() Цикл
		
			ВыборкаТовары = ВыборкаСклад.Выбрать();
			
			ОбОприход = Документы.ОприходованиеИзлишковТоваров.СоздатьДокумент();
			ОбОприход.Дата = КонецМесяца(ДатаЗакрытия);
			ОбОприход.Автор = ПараметрыСеанса.ТекущийПользователь;
			ОбОприход.Ответственный = ПараметрыСеанса.ТекущийПользователь;
			ОбОприход.Валюта = Валюта;
			ОбОприход.Организация = ВыборкаОрганизация.Организация;
			ОбОприход.Склад = ВыборкаСклад.Склад;
			ОбОприход.СтатьяДоходов = СтатьяДоходов;
			ОбОприход.НеДелатьДвиженияПоРезервамТоваровОрганизации = Истина;
			ОбОприход.Комментарий = "#Создано автоматически, при закрытии месяца " + Формат(ДатаЗакрытия, "ДФ='MMMM yyyy'");
			Пока ВыборкаТовары.Следующий() Цикл
			
				НовСтр = ОбОприход.Товары.Добавить();
				ЗаполнитьЗначенияСвойств(НовСтр, ВыборкаТовары);
				Если НовСтр.Цена = 0 Тогда
					
					ЦенаПоСебестоимости = ПолучитьЦенуТовараПоСебестоимости(ВыборкаТовары.Номенклатура, ВыборкаТовары.Характеристика, КонецМесяца(ДатаЗакрытия));
					
					Если ЦенаПоСебестоимости Тогда
						
						
						НовСтр.Цена = ЦенаПоСебестоимости;
						
					Иначе	
						
						НовСтр.Цена = 1;	
						
						Сообщение = Новый СообщениеПользователю;
						Сообщение.Текст = "Не удалось определить цену для " + ВыборкаТовары.Номенклатура + " " + ВыборкаТовары.Характеристика;
						Сообщение.Сообщить();
						
					КонецЕсли;
					
				КонецЕсли;
				НовСтр.Сумма = НовСтр.Цена * НовСтр.Количество;
				
			КонецЦикла;
			
			Если ОбОприход.Товары.Количество() Тогда
			
				Попытка
					ОбОприход.Записать(РежимЗаписиДокумента.Проведение);
				Исключение
					
					ОбОприход.Записать();
					
					Сообщение = Новый СообщениеПользователю;
					Сообщение.Текст = "Не удалось провести " + ОбОприход.Ссылка + ", по причине: " + ОписаниеОшибки();
					Сообщение.Сообщить();
					
				КонецПопытки;	
				
				НовДокСтр = ТабДанные.Добавить();
				НовДокСтр.ДокументОприходование = ОбОприход.Ссылка;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура СоздатьОприходования(Команда)
	СоздатьОприходованияНаСервере();
КонецПроцедуры 

&НаСервере
Процедура СформироватьДокументыСписаниеНаСервере()
    
	мСтатьяРасходов = ПланыВидовХарактеристик.СтатьиРасходов.ПогрешностьРасчетаСебестоимости;
	Для Каждого ТекСтрока Из ТабДанные Цикл
		
		Если ЗначениеЗаполнено(ТекСтрока.ДокументОприходование) Тогда
					
			ДокументОбъект = Документы.СписаниеНедостачТоваров.СоздатьДокумент();
			ДокументОбъект.Дата = КонецМесяца(ДатаЗакрытия) + 1;
			ДокументОбъект.ВидыЗапасовУказаныВручную = Истина;
			ДокументОбъект.Заполнить(ТекСтрока.ДокументОприходование);
			ДокументОбъект.Товары.Загрузить(ТекСтрока.ДокументОприходование.Товары.Выгрузить());
			ДокументОбъект.ВидыЗапасов.Загрузить(ТекСтрока.ДокументОприходование.Товары.Выгрузить());
			ДокументОбъект.СтатьяРасходов = мСтатьяРасходов;
			ДокументОбъект.НеДелатьДвиженияПоРезервамТоваровОрганизации = Истина;
			ДокументОбъект.Оприходование = ТекСтрока.ДокументОприходование;
			ДокументОбъект.Организация = ТекСтрока.ДокументОприходование.Организация;
			ДокументОбъект.Комментарий = "#Создано автоматически, при закрытии месяца " + Формат(ДатаЗакрытия, "ДФ='MMMM yyyy'") + ". По оприходованию №" + ТекСтрока.ДокументОприходование.Номер + " от " +  Формат(ТекСтрока.ДокументОприходование.Дата, "ДФ=dd.MM.yyyy");
			
			Попытка
				ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
			Исключение
				ДокументОбъект.Записать();
				Сообщение = Новый СообщениеПользователю;
				Сообщение.Текст = "Не удалось провести " + ДокументОбъект.Ссылка + ", по причине: " + ОписаниеОшибки();
				Сообщение.Сообщить();
			КонецПопытки;
			ТекСтрока.СписаниеТоваров = ДокументОбъект.Ссылка;
				
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьДокументыСписание(Команда)
	СформироватьДокументыСписаниеНаСервере();
КонецПроцедуры

&НаСервере
Функция ПолучитьЦенуТовараПоСебестоимости(Номенклатура, Характеристика, ТребуемыйПериод)

	Запрос5 = Новый Запрос("
		|ВЫБРАТЬ Первые 1
		|	ВЫБОР
		|		КОГДА СебестоимостьТоваровОбороты.КоличествоПриход = 0
		|			ТОГДА 0
		|		ИНАЧЕ ВЫРАЗИТЬ(СебестоимостьТоваровОбороты.СтоимостьПриход / СебестоимостьТоваровОбороты.КоличествоПриход КАК ЧИСЛО(15, 2))
		|	КОНЕЦ КАК Цена
		|ИЗ
		|	РегистрНакопления.СебестоимостьТоваров.Обороты(, , Регистратор, ) КАК СебестоимостьТоваровОбороты
		|ГДЕ
		|	СебестоимостьТоваровОбороты.АналитикаУчетаНоменклатуры.Номенклатура = &Номенклатура
		|	И СебестоимостьТоваровОбороты.АналитикаУчетаНоменклатуры.Характеристика = &Характеристика
		|	И ВЫБОР
		|			КОГДА СебестоимостьТоваровОбороты.КоличествоПриход = 0
		|				ТОГДА 0
		|			ИНАЧЕ ВЫРАЗИТЬ(СебестоимостьТоваровОбороты.СтоимостьПриход / СебестоимостьТоваровОбороты.КоличествоПриход КАК ЧИСЛО(15, 2))
		|		КОНЕЦ > 1
		|	И СебестоимостьТоваровОбороты.Период <= &Период
		|
		|УПОРЯДОЧИТЬ ПО
		|	СебестоимостьТоваровОбороты.Период УБЫВ");

	Запрос5.УстановитьПараметр("Номенклатура", Номенклатура);
	Запрос5.УстановитьПараметр("Период", ТребуемыйПериод);
	Запрос5.УстановитьПараметр("Характеристика", Характеристика);

	РезультатЗапроса = Запрос5.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	Если Выборка.Следующий()  Тогда
		Возврат Выборка.Цена;
	Иначе
		Возврат 0; 
	КонецЕсли;          

КонецФункции // ()
