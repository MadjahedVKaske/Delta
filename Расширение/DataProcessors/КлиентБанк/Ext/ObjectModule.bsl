
&ИзменениеИКонтроль("ВыгрузитьПлатежиПорциямиПоПравилам")
Процедура Buro1Goz_ВыгрузитьПлатежиПорциямиПоПравилам(ИнформацияОбОбъединенииФайлов, ИдФормы)
	
	ПотокиВыгрузки = ИнформацияОбОбъединенииФайлов.ПотокиВыгрузки;
	КоличествоСчетовВПравиле = ИнформацияОбОбъединенииФайлов.КоличествоСчетовВПравиле;
	
	Для Каждого ПотокВыгрузки Из ПотокиВыгрузки Цикл
		СтрокиКВыгрузке = ДокументыКВыгрузкеПоСчетам(ПотокВыгрузки.Счета);
		Если СтрокиКВыгрузке.Количество() Тогда
			ПараметрыВыгрузки = Новый Структура;
			ПараметрыВыгрузки.Вставить("БанковскийСчет", ПотокВыгрузки.Счета[0].Ссылка);
			ПараметрыВыгрузки.Вставить("НастройкаОбмена", ПотокВыгрузки.Правило);
			ПараметрыВыгрузки.Вставить("Кодировка", ПотокВыгрузки.Счета[0].Кодировка);
			ПараметрыВыгрузки.Вставить("Программа", ПотокВыгрузки.Счета[0].Программа);
			ПараметрыВыгрузки.Вставить("ФорматОбмена", ПотокВыгрузки.Счета[0].ФорматОбмена);
			ПараметрыВыгрузки.Вставить("ВерсияФорматаВыгрузки", ПотокВыгрузки.Счета[0].ВерсияФорматаВыгрузки);
			
			ТаблицаДокументов = ДокументыКВыгрузке.Выгрузить(СтрокиКВыгрузке);
			#Вставка
			Для Каждого СтрокаДок Из ТаблицаДокументов Цикл 
				
				Длинна = СтрДлина(СтрокаДок.Номер);
				Итог = "";
				Для Инд = 1 По Длинна Цикл
					Символ = Сред(СтрокаДок.Номер,Инд,1);
					КодСимвола = КодСимвола(Символ);
					Если КодСимвола > 47 И КодСимвола < 58 Тогда
						Итог = Итог+Символ;
					КонецЕсли;
				КонецЦикла;
				
				СтрокаДок.Номер = Итог;
				
			КонецЦикла;
			#КонецВставки
			ПараметрыВыгрузки.Вставить("ТаблицаДокументов", ТаблицаДокументов);
			
			ИзменитьРеквизитыСчетовПотока(ПотокВыгрузки.Счета,
				Новый Структура("Выгружен, АдресХранилищаДокументов", Ложь, ПоместитьВоВременноеХранилище(ТаблицаДокументов, ИдФормы)));
				
			ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(ИдФормы);
			ПараметрыВыполнения.ОжидатьЗавершение = 0;
			ПараметрыВыполнения.НаименованиеФоновогоЗадания =
				НСтр("ru = 'Выгрузка платежей в банк'");
		
			Результат = ДлительныеОперации.ВыполнитьВФоне(
				"Обработки.КлиентБанк.Выгрузить",
				ПараметрыВыгрузки,
				ПараметрыВыполнения);
			
			ИзменитьРеквизитыСчетовПотока(ПотокВыгрузки.Счета, 
				Новый Структура("АдресХранилищаФайла", Результат.АдресРезультата));
			
			Если Результат.Статус = "Выполнено" Тогда
				ИзменитьРеквизитыСчетовПотока(ПотокВыгрузки.Счета, 
					Новый Структура("СохранитьФайл", Истина));
			Иначе
				ИзменитьРеквизитыСчетовПотока(ПотокВыгрузки.Счета, 
					Новый Структура("Выгружается, ИдентификаторВыгрузки, ДанныеВыгрузки",
						Истина, Результат.ИдентификаторЗадания, ПоместитьВоВременноеХранилище(Результат, ИдФормы)));
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры
