
&НаСервере
&ИзменениеИКонтроль("ВывестиРодительскиеОбъекты")
Процедура Buro1Goz_ВывестиРодительскиеОбъекты(ТекущийОбъект, ДеревоРодитель, ВыведенныеОбъекты, СлужебныеОбъекты, ИндексСвязейОбъектов)

	МетаданныеОбъекта = ТекущийОбъект.Метаданные();
	СписокРеквизитов  = Новый Массив;

	Если СлужебныеОбъекты = Неопределено Тогда 
		СлужебныеОбъекты = Новый Соответствие;
	КонецЕсли;

	Если ИндексСвязейОбъектов = Неопределено Тогда 
		ИндексСвязейОбъектов = Новый Соответствие;
	КонецЕсли;
	
	#Вставка
	Если СлужебныеОбъекты["Buro1Goz_ДобавленныеМетаданные"] = Неопределено Тогда
		СлужебныеОбъекты.Вставить("Buro1Goz_ДобавленныеМетаданные", Buro1Goz_ПолучитьДобавленныеМетаданные());
	КонецЕсли;
	Buro1Goz_ДобавленныеМетаданные = СлужебныеОбъекты["Buro1Goz_ДобавленныеМетаданные"];
	#КонецВставки
	Для Каждого Реквизит Из МетаданныеОбъекта.Реквизиты Цикл

		#Удаление
		Если Не Метаданные.КритерииОтбора.СвязанныеДокументы.Состав.Содержит(Реквизит) Тогда
		#КонецУдаления
		#Вставка
		Если Не Buro1Goz_СвязанныеДокументыСодержитРеквизит(Реквизит, Buro1Goz_ДобавленныеМетаданные) Тогда
		#КонецВставки
			Продолжить;
		КонецЕсли;

		Для Каждого ТекущийТип Из Реквизит.Тип.Типы() Цикл

			МетаданныеРеквизита = МетаданныеТипаРеквизита(ТекущийТип);
			Если МетаданныеРеквизита.Метаданные = Неопределено Тогда
				Продолжить;
			КонецЕсли;

			ЗначениеРеквизита = ТекущийОбъект[Реквизит.Имя];
			Если ЗначениеЗаполнено(ЗначениеРеквизита)
				И ТипЗнч(ЗначениеРеквизита) = ТекущийТип
				И ЗначениеРеквизита <> ТекущийОбъект
				И СписокРеквизитов.Найти(ЗначениеРеквизита) = Неопределено Тогда

				СписокРеквизитов.Добавить(ЗначениеРеквизита);
			КонецЕсли;
		КонецЦикла;

	КонецЦикла;

	Для Каждого ТабличнаяЧасть Из МетаданныеОбъекта.ТабличныеЧасти Цикл

		ИменаРеквизитов = "";
		СодержимоеТЧ = ТекущийОбъект[ТабличнаяЧасть.Имя].Выгрузить(); // ТаблицаЗначений
		Для Каждого Реквизит Из ТабличнаяЧасть.Реквизиты Цикл

			#Удаление
			Если Не Метаданные.КритерииОтбора.СвязанныеДокументы.Состав.Содержит(Реквизит) Тогда
			#КонецУдаления
			#Вставка
			Если Не Buro1Goz_СвязанныеДокументыСодержитРеквизит(Реквизит, Buro1Goz_ДобавленныеМетаданные) Тогда
			#КонецВставки
				Продолжить;
			КонецЕсли;

			Для Каждого ТекущийТип Из Реквизит.Тип.Типы() Цикл
				МетаданныеРеквизита = МетаданныеТипаРеквизита(ТекущийТип);
				Если МетаданныеРеквизита.Метаданные = Неопределено Тогда
					Продолжить;
				КонецЕсли;

				ИменаРеквизитов = ИменаРеквизитов + ?(ИменаРеквизитов = "", "", ", ") + Реквизит.Имя;
				Прервать;
			КонецЦикла;

		КонецЦикла;

		СодержимоеТЧ.Свернуть(ИменаРеквизитов);
		Для Каждого КолонкаТЧ Из СодержимоеТЧ.Колонки Цикл

			Для Каждого СтрокаТЧ Из СодержимоеТЧ Цикл

				ЗначениеРеквизита = СтрокаТЧ[КолонкаТЧ.Имя];
				МетаданныеЗначения = МетаданныеТипаРеквизита(ТипЗнч(ЗначениеРеквизита));
				Если МетаданныеЗначения.Метаданные = Неопределено Тогда
					Продолжить;
				КонецЕсли;

				Если ЗначениеРеквизита = ТекущийОбъект
					Или СписокРеквизитов.Найти(ЗначениеРеквизита) <> Неопределено Тогда
					Продолжить;
				КонецЕсли;

				СписокРеквизитов.Добавить(ЗначениеРеквизита);
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;

	Если СписокРеквизитов.Количество() > 0 Тогда
		ВыводимыеОбъекты = ЗапросПоРеквизитамОбъектов(СписокРеквизитов).Выполнить().Выгрузить();
		ВыводимыеОбъекты.Сортировать("Дата");
		Для каждого ВыводимыйОбъект Из ВыводимыеОбъекты Цикл 

			Если ИндексСвязейОбъектов[ТекущийОбъект] = ВыводимыйОбъект.Ссылка Тогда 
				Продолжить;
			КонецЕсли;

			ИндексСвязейОбъектов[ТекущийОбъект] = ВыводимыйОбъект.Ссылка;

			НоваяСтрока = ДобавитьСтрокуВДерево(ДеревоРодитель, ВыводимыйОбъект, ВыведенныеОбъекты);			
			Если НоваяСтрока <> Неопределено
				И Не ДобавляемыйОбъектИмеетсяСредиРодителей(ДеревоРодитель, ВыводимыйОбъект.Ссылка) Тогда

				// @skip-check query-in-loop - Рекурсивный алгоритм обработки дерева.
				ВывестиРодительскиеОбъекты(ВыводимыйОбъект.Ссылка, НоваяСтрока, ВыведенныеОбъекты,
				СлужебныеОбъекты, ИндексСвязейОбъектов);

			ИначеЕсли СлужебныеОбъекты[ВыводимыйОбъект.Ссылка] = Неопределено Тогда 

				СлужебныеОбъекты[ВыводимыйОбъект.Ссылка] = Истина;
				// @skip-check query-in-loop - Рекурсивный алгоритм обработки дерева.
				ВывестиРодительскиеОбъекты(ВыводимыйОбъект.Ссылка, ДеревоРодитель, ВыведенныеОбъекты,
				СлужебныеОбъекты, ИндексСвязейОбъектов);

			КонецЕсли;
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры

&НаСервере
&ИзменениеИКонтроль("ОбъектыПоКритериюОтбора")
Функция Buro1Goz_ОбъектыПоКритериюОтбора(ЗначениеКритерияОтбора)
	
	ШаблонЗапроса = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПсевдонимТаблицы.Ссылка КАК Ссылка
	|ИЗ
	|	ИмяТаблицы КАК ПсевдонимТаблицы
	|ГДЕ
	|	ПсевдонимТаблицы.ИмяРеквизита = &ЗначениеКритерияОтбора";
	
	ШаблонЗапросаОбъединения = "ВЫБРАТЬ
	|	ПсевдонимТаблицы.Ссылка КАК Ссылка
	|ИЗ
	|	ИмяТаблицы КАК ПсевдонимТаблицы
	|ГДЕ
	|	ПсевдонимТаблицы.ИмяРеквизита = &ЗначениеКритерияОтбора";
	
	ЧастиЗапроса = Новый Массив;
	ТекстЧастиЗапроса = "";

	Для Каждого ЭлементСостава Из Метаданные.КритерииОтбора.СвязанныеДокументы.Состав Цикл

		#Удаление
		Если НЕ ЭлементСостава.Тип.СодержитТип(ТипЗнч(ЗначениеКритерияОтбора)) Тогда
		#КонецУдаления
		#Вставка
		Если Не Buro1Goz_СвязанныеДокументыСоставСодержитТип(ЭлементСостава, ТипЗнч(ЗначениеКритерияОтбора)) Тогда
		#КонецВставки
			Продолжить;
		КонецЕсли;

		ПутьКДанным = ЭлементСостава.ПолноеИмя();

		Если СтрНайти(ПутьКДанным, "ТабличнаяЧасть") Тогда
			ОбъектМетаданных = ЭлементСостава.Родитель().Родитель();
		Иначе
			ОбъектМетаданных = ЭлементСостава.Родитель();
		КонецЕсли;

		Если НЕ ПравоДоступа("Чтение", ОбъектМетаданных) Тогда
			Продолжить;
		КонецЕсли;

		Точка = СтрНайти(ПутьКДанным, ".", НаправлениеПоиска.СКонца);
		ИмяРеквизита = Сред(ПутьКДанным, Точка + 1);

		ИмяТаблицы = ЭлементСостава.Родитель().ПолноеИмя();
		ИмяТаблицы = СтрЗаменить(ИмяТаблицы, "ТабличнаяЧасть.", "");

		Точка = СтрНайти(ИмяТаблицы, ".", НаправлениеПоиска.СКонца);
		ПсевдонимТаблицы = "Таблица_" + Сред(ИмяТаблицы, Точка + 1);

		ТекстЧастиЗапроса = ?(ТекстЧастиЗапроса = "", ШаблонЗапроса, ШаблонЗапросаОбъединения);
		ТекстЧастиЗапроса = СтрЗаменить(ТекстЧастиЗапроса, "ИмяТаблицы", ИмяТаблицы);
		ТекстЧастиЗапроса = СтрЗаменить(ТекстЧастиЗапроса, "ПсевдонимТаблицы", ПсевдонимТаблицы);
		ТекстЧастиЗапроса = СтрЗаменить(ТекстЧастиЗапроса, "ИмяРеквизита", ИмяРеквизита);

		ЧастиЗапроса.Добавить(ТекстЧастиЗапроса);

	КонецЦикла;

	#Вставка
	Если ЗначениеЗаполнено(ЭтаФорма.Buro1Goz_ДанныеСвязанныхДокументовТекстЗапроса) Тогда
		
		Если ЗначениеКритерияОтбора.Метаданные().РасширениеКонфигурации() <> Неопределено Тогда
			ЧастиЗапроса = Новый Массив;
		КонецЕсли;
		ЧастиЗапроса.Добавить(ЭтаФорма.Buro1Goz_ДанныеСвязанныхДокументовТекстЗапроса);
		//Запрос.Текст = СтрСоединить(МассивЗапросов, Символы.ПС + "ОБЪЕДИНИТЬ" + Символы.ПС);
			
	КонецЕсли;
	#КонецВставки
	
	Если ЧастиЗапроса.Количество() > 0 Тогда
		Запрос = Новый Запрос;
		Разделитель = Символы.ПС + "ОБЪЕДИНИТЬ" + Символы.ПС;
		Запрос.Текст = СтрСоединить(ЧастиЗапроса, Разделитель);
		Запрос.УстановитьПараметр("ЗначениеКритерияОтбора", ЗначениеКритерияОтбора);
		Возврат Запрос.Выполнить().Выгрузить();
	Иначе
		Возврат Новый ТаблицаЗначений;
	КонецЕсли;

КонецФункции

&НаСервере
Функция Buro1Goz_СвязанныеДокументыСодержитРеквизит(Реквизит, ДобавленныеМетаданные)
	
	Если Метаданные.КритерииОтбора.СвязанныеДокументы.Состав.Содержит(Реквизит) Тогда
		Возврат Истина;
	Иначе
		
		ЕстьВСвязанныхДокументах = Не ДобавленныеМетаданные.Найти(Реквизит) = Неопределено;
		Возврат ЕстьВСвязанныхДокументах;
		
	КонецЕсли;	
	
КонецФункции

&НаСервере
Функция Buro1Goz_СвязанныеДокументыСодержитТип(Тип)
	
	Если Метаданные.КритерииОтбора.СвязанныеДокументы.Тип.СодержитТип(Тип) Тогда
		Возврат Истина;
	Иначе
		
		ТипНайден = Не ЭтаФорма.Buro1Goz_ДанныеСвязанныхДокументовТипы.НайтиПоЗначению(Тип) = Неопределено;
		Возврат ТипНайден;
		
	КонецЕсли;
	
КонецФункции

&НаСервере
Функция Buro1Goz_СвязанныеДокументыСоставСодержитТип(ЭлементСостава, Тип)
	
	Если ЭлементСостава.Тип.СодержитТип(Тип) Тогда
		Возврат Истина;
	Иначе
		ТипНайден = Не ЭтаФорма.Buro1Goz_ДанныеСвязанныхДокументовТипы.НайтиПоЗначению(Тип) = Неопределено;
		Возврат ТипНайден;
	КонецЕсли;
	
КонецФункции


&НаСервере
Процедура Buro1Goz_СоздатьДанныеСвязанныхДокументов()
	
	Если Не ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ЭтаФорма, "Buro1Goz_ДанныеСвязанныхДокументовМетаданные") Тогда
		
		НовыеРеквизиты = Новый Массив;
		НовыеРеквизиты.Добавить(Новый РеквизитФормы("Buro1Goz_ДанныеСвязанныхДокументовМетаданные", Новый ОписаниеТипов("СписокЗначений")));
		НовыеРеквизиты.Добавить(Новый РеквизитФормы("Buro1Goz_ДанныеСвязанныхДокументовТипы", Новый ОписаниеТипов("СписокЗначений")));
		НовыеРеквизиты.Добавить(Новый РеквизитФормы("Buro1Goz_ДанныеСвязанныхДокументовТекстЗапроса", Новый ОписаниеТипов("Строка")));
		ИзменитьРеквизиты(НовыеРеквизиты);
		
		ШаблонЗапроса = 
			"ВЫБРАТЬ
			|	Таблица.Ссылка
			|ИЗ
			|	&Таблица КАК Таблица
			|ГДЕ
			|	&Отбор = &ЗначениеКритерияОтбора";
		ТекстЗапроса = "";
		МассивЗапросов = Новый Массив;
		
		ДанныеСвязанныхДокументов = СтруктураПодчиненностиСлужебный.Buro1Goz_ПолучитьДанныеСвязанныхДокументов();
		Для каждого ДанныеПоОбъектуМетаданных Из ДанныеСвязанныхДокументов Цикл
			
			ЭтаФорма.Buro1Goz_ДанныеСвязанныхДокументовМетаданные.Добавить(ДанныеПоОбъектуМетаданных.ПолноеИмя);
			
			МетаданныеДерево = СтрРазделить(ДанныеПоОбъектуМетаданных.ПолноеИмя, ".");
			ЭтаФорма.Buro1Goz_ДанныеСвязанныхДокументовТипы.Добавить(Тип(МетаданныеДерево[0] + "Ссылка." + МетаданныеДерево[1]));
			
			ТаблицаМассив = Новый Массив;
			ТаблицаМассив.Добавить(МетаданныеДерево[0]);
			ТаблицаМассив.Добавить(МетаданныеДерево[1]);
			
			//Если это реквизит ТЧ
			Если МетаданныеДерево.Количество() = 6 Тогда
				ТаблицаМассив.Добавить(МетаданныеДерево[3]);
			КонецЕсли;
			
			ТекстЗапроса = СтрЗаменить(ШаблонЗапроса, "&Таблица", СтрСоединить(ТаблицаМассив, "."));
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Отбор", "Таблица." + МетаданныеДерево[МетаданныеДерево.ВГраница()]);
			МассивЗапросов.Добавить(ТекстЗапроса);
			
		КонецЦикла;
		ЭтаФорма.Buro1Goz_ДанныеСвязанныхДокументовТекстЗапроса = СтрСоединить(МассивЗапросов, Символы.ПС + "ОБЪЕДИНИТЬ" + Символы.ПС);
		
	КонецЕсли;

КонецПроцедуры

&НаСервере
Функция Buro1Goz_ПолучитьДобавленныеМетаданные()

	Buro1Goz_СоздатьДанныеСвязанныхДокументов();
	ДобавленныеМетаданные = Новый Массив;
	Для каждого ПолноеИмя Из ЭтаФорма.Buro1Goz_ДанныеСвязанныхДокументовМетаданные Цикл
		ДобавленныеМетаданные.Добавить(ОбщегоНазначения.ОбъектМетаданныхПоПолномуИмени(ПолноеИмя));
	КонецЦикла;
	
	Возврат ДобавленныеМетаданные;

КонецФункции