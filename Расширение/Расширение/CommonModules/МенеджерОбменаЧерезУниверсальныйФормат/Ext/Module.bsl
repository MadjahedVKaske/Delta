
&ИзменениеИКонтроль("ПКО_Документ_СборкаТоваров_ПриОтправкеДанных")
Процедура Buro1Goz_ПКО_Документ_СборкаТоваров_ПриОтправкеДанных(ДанныеИБ, ДанныеXDTO, КомпонентыОбмена, СтекВыгрузки)
	Если СтекВыгрузки.Количество() > 1 Тогда
		Возврат;
	КонецЕсли;
	Если ВерсияФорматаЧислом(КомпонентыОбмена.ВерсияФорматаОбмена) >= ВерсияФорматаЧислом("1.10") Тогда
		УстановитьПравилоДляПКСТабЧасти(КомпонентыОбмена,
										"Документ_СборкаТоваров",
										"Товары",
										"НомерГТД",
										Истина,
										"Справочник_НомераГТД_Отправка");
	КонецЕсли;
									
	СкладДляВыгрузки = СкладДляВыгрузки(ДанныеИБ.Склад, КомпонентыОбмена.ПараметрыКонвертации);
	ДанныеXDTO.Вставить("Склад", СкладДляВыгрузки);
	
	Если ЗначениеЗаполнено(ДанныеИБ.Характеристика)
		И ВерсияФорматаЧислом(КомпонентыОбмена.ВерсияФорматаОбмена) >= ВерсияФорматаЧислом("1.6") Тогда
	
		ВладелецХарактеристики = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеИБ.Характеристика, "Владелец");
		ВидНоменклатуры = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеИБ.Номенклатура, "ВидНоменклатуры");
		
		Инструкция = Новый Структура;
		Если ВладелецХарактеристики = ДанныеИБ.Номенклатура
			Или (ВладелецХарактеристики = ВидНоменклатуры
			И ВерсияФорматаЧислом(КомпонентыОбмена.ВерсияФорматаОбмена) >= ВерсияФорматаЧислом("1.7")) Тогда
			
			Инструкция.Вставить("Значение", ДанныеИБ.Характеристика);
			Инструкция.Вставить("ИмяПКО", "Справочник_ХарактеристикиНоменклатуры");
		Иначе
			Инструкция.Вставить("ИмяПКО", "Справочник_ХарактеристикиНоменклатуры_ИзСтруктуры");
			
			Характеристика = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			ДанныеИБ.Характеристика, "Наименование, НаименованиеПолное");
			
			Если ВерсияФорматаЧислом(КомпонентыОбмена.ВерсияФорматаОбмена) = ВерсияФорматаЧислом("1.6") Тогда
				Характеристика.Вставить("Владелец", ДанныеИБ.Номенклатура);
			Иначе
				Характеристика.Вставить("Владелец", ВидНоменклатуры);
			КонецЕсли;
			
			Инструкция.Вставить("Значение", Характеристика);
			
			ПравилоОбработки = КомпонентыОбмена.ПравилаОбработкиДанных.Найти("Справочник_ХарактеристикиНоменклатуры_Отправка", "Имя");
			Если Не ПравилоОбработки = Неопределено Тогда
				ОбменДаннымиXDTOСервер.ВыгрузкаОбъектаВыборки(КомпонентыОбмена, Характеристика, ПравилоОбработки);
			КонецЕсли;
			
		КонецЕсли;
		
		ДанныеXDTO.Вставить("Характеристика", Инструкция);
	КонецЕсли;
	
	ВыгрузитьПодразделениеИзРеквизитаДокумента(КомпонентыОбмена, ДанныеИБ, ДанныеXDTO, "Склад");
	ВыгрузитьДополнительныеРеквизитыИСведения(КомпонентыОбмена, ДанныеИБ, ДанныеXDTO);
	
	ТекстЗапроса = "ВЫБРАТЬ
		|	Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения
		|ИЗ
		|	Справочник.Номенклатура КАК Номенклатура
		|ГДЕ
		|	Номенклатура.Ссылка = &НоменклатураСсылка";
	
	Если ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.СборкаТоваров Тогда
		
		ДанныеXDTO.Вставить("ВидОперации", "Сборка");
		ДанныеXDTO.Вставить("ТипЗапасов", "СобственныеТовары"); //@NON-NLS-2
		
		ТекстЗапроса = ТекстЗапроса + ОбщегоНазначения.РазделительПакетаЗапросов() +
			"ВЫБРАТЬ
			|	СборкаТоваровТовары.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
			|	СУММА(СборкаТоваровТовары.ДоляСтоимости) КАК ДоляСтоимости,
			|	СУММА(СборкаТоваровТовары.Количество) КАК Количество
			|ПОМЕСТИТЬ втТовары
			|ИЗ
			|	Документ.СборкаТоваров.Товары КАК СборкаТоваровТовары
			|ГДЕ
			|	СборкаТоваровТовары.Ссылка = &Ссылка
			|
			|СГРУППИРОВАТЬ ПО
			|	СборкаТоваровТовары.АналитикаУчетаНоменклатуры
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ЕСТЬNULL(КлючиАналитикиУчетаНоменклатуры.Номенклатура, ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)) КАК Номенклатура,
			|	ЕСТЬNULL(СпрНоменклатура.ЕдиницаИзмерения, ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)) КАК ЕдиницаИзмерения,
			|	&ТекстЗапросаХарактеристика,
			|	&ТекстЗапросаСерия,
			|	&ТекстЗапросаРНПТ,
			|	&ТекстЗапросаГТД,
			|	ВидыЗапасов.Количество КАК Количество,
			|	ВЫРАЗИТЬ(втТовары.ДоляСтоимости / втТовары.Количество * ВидыЗапасов.Количество КАК ЧИСЛО(10, 0)) КАК ДоляСтоимости,
			|	ВЫБОР
			|		КОГДА ВидыЗапасов.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
			|			ТОГДА ВЫРАЗИТЬ(""КомиссионныеТовары"" КАК СТРОКА(20))
			|		ИНАЧЕ ВЫРАЗИТЬ(&СобственныеТовары КАК СТРОКА(20))
			|	КОНЕЦ КАК ТипЗапасов,
			|	ВидыЗапасов.ИдентификаторСтроки КАК ИдентификаторСтроки
			|ИЗ
			|	Документ.СборкаТоваров.ВидыЗапасовСписание КАК ВидыЗапасов
			|		ЛЕВОЕ СОЕДИНЕНИЕ втТовары КАК втТовары
			|		ПО ВидыЗапасов.АналитикаУчетаНоменклатуры = втТовары.АналитикаУчетаНоменклатуры
			|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК КлючиАналитикиУчетаНоменклатуры
			|		ПО ВидыЗапасов.АналитикаУчетаНоменклатуры = КлючиАналитикиУчетаНоменклатуры.Ссылка
			|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
			|		ПО (КлючиАналитикиУчетаНоменклатуры.Номенклатура = СпрНоменклатура.Ссылка)
			|ГДЕ
			|	ВидыЗапасов.Ссылка = &Ссылка";
		
		ЗаполнитьСобственныеТоварыВТексте(ТекстЗапроса);
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаХарактеристика",
			ПолучитьТекстЗапросаХарактеристики(КомпонентыОбмена, "КлючиАналитикиУчетаНоменклатуры"));
			
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаСерия",
			ПолучитьТекстЗапросаСерии(КомпонентыОбмена, "КлючиАналитикиУчетаНоменклатуры"));
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаРНПТ",
			ПолучитьТекстЗапросаРНПТ(КомпонентыОбмена, "ВидыЗапасов", "КлючиАналитикиУчетаНоменклатуры"));
	
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаГТД",
			ПолучитьТекстЗапросаГТД(КомпонентыОбмена, "ВидыЗапасов"));
			
	Иначе
		
		ДанныеXDTO.Вставить("ВидОперации", "Разборка");
		
		ТекстЗапроса = ТекстЗапроса + ОбщегоНазначения.РазделительПакетаЗапросов() +
			"ВЫБРАТЬ
			|	Товары.Номенклатура КАК Номенклатура,
			|	Товары.Характеристика КАК Характеристика,
			|	СУММА(Товары.Количество) КАК Количество,
			|	СУММА(Товары.ДоляСтоимости) КАК ДоляСтоимости,
			|	Товары.Серия КАК Серия,
			|	Товары.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
			|	Товары.ИдентификаторСтроки КАК ИдентификаторСтроки
			|ПОМЕСТИТЬ втТовары
			|ИЗ
			|	Документ.СборкаТоваров.Товары КАК Товары
			|ГДЕ
			|	Товары.Ссылка = &Ссылка
			|
			|СГРУППИРОВАТЬ ПО
			|	Товары.Серия,
			|	Товары.Номенклатура,
			|	Товары.АналитикаУчетаНоменклатуры,
			|	Товары.Характеристика,
			|	Товары.ИдентификаторСтроки
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ВидыЗапасов.АналитикаУчетаНоменклатуры.Номенклатура КАК Номенклатура,
			|	ВидыЗапасов.АналитикаУчетаНоменклатуры.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
			|	&ТекстЗапросаХарактеристика,
			|	&ТекстЗапросаСерия,
			|	&ТекстЗапросаРНПТ,
			|	&ТекстЗапросаГТД,
			|	ВидыЗапасов.Количество КАК Количество,
			|	ВЫРАЗИТЬ(втТовары.ДоляСтоимости * ВидыЗапасов.Количество / втТовары.Количество КАК ЧИСЛО(10, 0)) КАК ДоляСтоимости,
			|	ВЫБОР
			|		КОГДА ВидыЗапасов.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
			|			ТОГДА ВЫРАЗИТЬ(""КомиссионныеТовары"" КАК СТРОКА(20))
			|		ИНАЧЕ ВЫРАЗИТЬ(&СобственныеТовары КАК СТРОКА(20))
			|	КОНЕЦ КАК ТипЗапасов,
			|	втТовары.ИдентификаторСтроки
			|ИЗ
			|	втТовары КАК втТовары
			|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СборкаТоваров.ВидыЗапасовОприходование КАК ВидыЗапасов
			|		ПО втТовары.АналитикаУчетаНоменклатуры = ВидыЗапасов.АналитикаУчетаНоменклатуры
			|ГДЕ
			|	ВидыЗапасов.Ссылка = &Ссылка
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ ПЕРВЫЕ 1
			|	ВЫБОР
			|		КОГДА ВидыЗапасовСписание.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
			|			ТОГДА ВЫРАЗИТЬ(""КомиссионныеТовары"" КАК СТРОКА(20))
			|		ИНАЧЕ ВЫРАЗИТЬ(&СобственныеТовары КАК СТРОКА(20))
			|	КОНЕЦ КАК ТипЗапасов
			|ИЗ
			|	Документ.СборкаТоваров.ВидыЗапасовСписание КАК ВидыЗапасовСписание
			|ГДЕ
			|	ВидыЗапасовСписание.Ссылка = &Ссылка";
		
		ЗаполнитьСобственныеТоварыВТексте(ТекстЗапроса);
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаХарактеристика",
			ПолучитьТекстЗапросаХарактеристики(КомпонентыОбмена, "ВидыЗапасов.АналитикаУчетаНоменклатуры"));
			
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаСерия",
			ПолучитьТекстЗапросаСерии(КомпонентыОбмена, "ВидыЗапасов.АналитикаУчетаНоменклатуры"));
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаРНПТ",
			ПолучитьТекстЗапросаРНПТ(КомпонентыОбмена, "ВидыЗапасов", "ВидыЗапасов.АналитикаУчетаНоменклатуры"));
	
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаГТД",
			ПолучитьТекстЗапросаГТД(КомпонентыОбмена, "ВидыЗапасов"));
				
	КонецЕсли;
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Ссылка",             ДанныеИБ.Ссылка);
	Запрос.УстановитьПараметр("НоменклатураСсылка", ДанныеИБ.Номенклатура);
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	ДанныеНоменклатуры = РезультатЗапроса[0].Выбрать();
	Если ДанныеНоменклатуры.Следующий() Тогда
		ДанныеXDTO.Вставить("ЕдиницаИзмерения", ДанныеНоменклатуры.ЕдиницаИзмерения);
	Иначе
		ДанныеXDTO.Вставить("ЕдиницаИзмерения", Справочники.УпаковкиЕдиницыИзмерения.ПустаяСсылка());
	КонецЕсли;
	
	Если ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.РазборкаТоваров Тогда
		
		ДанныеВидовЗапасов = РезультатЗапроса[РезультатЗапроса.Количество() - 1].Выбрать();
		
		Если ДанныеВидовЗапасов.Следующий() Тогда
			ДанныеXDTO.Вставить("ТипЗапасов", ДанныеВидовЗапасов.ТипЗапасов);
		Иначе
			ДанныеXDTO.Вставить("ТипЗапасов", "СобственныеТовары"); //@NON-NLS-2
		КонецЕсли;
		
		Товары = РезультатЗапроса[РезультатЗапроса.Количество() - 2].Выгрузить();
		ЗаполнитьХарактеристикуУпаковкуВТЧ(КомпонентыОбмена, Товары, Истина, Ложь);
		
	Иначе
		// Сборка.
		Товары = РезультатЗапроса[РезультатЗапроса.Количество() - 1].Выгрузить();
		ЗаполнитьХарактеристикуУпаковкуВТЧ(КомпонентыОбмена, Товары, Истина, Ложь);
		#Вставка
		// Убираем комплектующие
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	КомплектующиеУправленческие.Номенклатура КАК Номенклатура
		|ИЗ
		|	Справочник.ВариантыКомплектацииНоменклатуры.КомплектующиеУправленческие КАК КомплектующиеУправленческие
		|ГДЕ
		|	КомплектующиеУправленческие.Ссылка = &ВариантКомплектации";
		
		Запрос.УстановитьПараметр("ВариантКомплектации", ДанныеИБ.Ссылка.ВариантКомплектации);
		
		МассивСтрокУдаления = Новый Массив;
		
		РезультатЗапроса = Запрос.Выполнить();
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			СтрОтбора = Новый Структура;
			СтрОтбора.Вставить("Номенклатура", Выборка.Номенклатура);
			НСтроки = Товары.НайтиСтроки(СтрОтбора);
			
			Для Каждого НСтрока Из НСтроки Цикл
				МассивСтрокУдаления.Добавить(НСтрока);
			КонецЦикла;
		КонецЦикла;
		
		Для Каждого СтрокаУдаления Из МассивСтрокУдаления Цикл
			Товары.Удалить(СтрокаУдаления);
		КонецЦикла;
		#КонецВставки
	КонецЕсли;
	
	ДанныеXDTO.Вставить("Товары", Товары);
	//Сведения РНПТ
	
	//++ Локализация
	Если ВерсияФорматаЧислом(КомпонентыОбмена.ВерсияФорматаОбмена) >= ВерсияФорматаЧислом("1.16") Тогда
		
		Если ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.СборкаТоваров Тогда
			ИмяТаблицы = "Документ.СборкаТоваров.ВидыЗапасовСписание";
			ИдентификаторыИзВидовЗапасов = Истина;
		Иначе
			ИмяТаблицы = "Документ.СборкаТоваров.ВидыЗапасовОприходование";
			ИдентификаторыИзВидовЗапасов = Ложь;
		КонецЕсли;
		
		ПолучитьСведенияОПрослеживаемости(ИмяТаблицы, ДанныеИБ, ДанныеXDTO, КомпонентыОбмена, ИдентификаторыИзВидовЗапасов);  
	КонецЕсли;
	//-- Локализация
КонецПроцедуры

&ИзменениеИКонтроль("ПКО_Документ_ПеремещениеТоваров_Отправка_ПриОтправкеДанных")
Процедура Buro1Goz_ПКО_Документ_ПеремещениеТоваров_Отправка_ПриОтправкеДанных(ДанныеИБ, ДанныеXDTO, КомпонентыОбмена, СтекВыгрузки)
	Если СтекВыгрузки.Количество() > 1 Тогда
		Возврат;
	КонецЕсли;

	Если ВерсияФорматаЧислом(КомпонентыОбмена.ВерсияФорматаОбмена) >= ВерсияФорматаЧислом("1.10") Тогда
		УстановитьПравилоДляПКСТабЧасти(КомпонентыОбмена,
		"Документ_ПеремещениеТоваров_Отправка", 
		"Товары", 
		"НомерГТД",
		Истина,
		"Справочник_НомераГТД_Отправка");
	КонецЕсли;
	Если ВерсияФорматаЧислом(КомпонентыОбмена.ВерсияФорматаОбмена) >= ВерсияФорматаЧислом("1.17") Тогда
		УстановитьПравилоДляПКСТабЧасти(КомпонентыОбмена,
		"Документ_ПеремещениеТоваров_Отправка", 
		"Товары", 
		"СтавкаНДСВРознице",
		Истина,
		"СправочникСсылка_СтавкиНДС_Отправка");
	КонецЕсли;
	ВыгрузитьДополнительныеРеквизитыИСведения(КомпонентыОбмена, ДанныеИБ, ДанныеXDTO);

	ВыгрузитьНалогообложениеНДС(КомпонентыОбмена, ДанныеИБ, ДанныеXDTO, Ложь);

	// Подразделения.
	ДанныеXDTO.Вставить("ПодразделениеОтправитель", Неопределено);
	ДанныеXDTO.Вставить("ПодразделениеПолучатель",  Неопределено);

	Если ЗначениеЗаполнено(ДанныеИБ.Организация) Тогда

		СкладОтправитель                      = ДанныеИБ.СкладОтправитель;
		СкладПолучатель                       = ДанныеИБ.СкладПолучатель;
		СоответствиеДляПолученияПодразделений = Неопределено;
		ТекПодразделениеОтправитель           = Неопределено;
		ТекПодразделениеПолучатель            = Неопределено;
		ДополнитьСоответствие                 = Ложь;

		КомпонентыОбмена.ПараметрыКонвертации.Свойство("СоответствиеДляПолученияПодразделений", СоответствиеДляПолученияПодразделений);

		Если СоответствиеДляПолученияПодразделений = Неопределено Тогда
			СоответствиеДляПолученияПодразделений = Новый Соответствие;
			ДополнитьСоответствие = Истина;
		Иначе 
			ТекПодразделениеОтправитель = СоответствиеДляПолученияПодразделений.Получить(СкладОтправитель);
			ТекПодразделениеПолучатель = СоответствиеДляПолученияПодразделений.Получить(СкладПолучатель);
		КонецЕсли;

		Если ТекПодразделениеОтправитель = Неопределено Тогда
			ТекПодразделениеОтправитель = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СкладОтправитель, "Подразделение");
			ДополнитьСоответствие = Истина;
		КонецЕсли;

		Если ТекПодразделениеПолучатель = Неопределено Тогда
			ТекПодразделениеПолучатель = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СкладПолучатель, "Подразделение");
			ДополнитьСоответствие = Истина;
		КонецЕсли;

		Если ЗначениеЗаполнено(ТекПодразделениеОтправитель) Тогда

			РегистрацияПодразделенияВНалоговомОргане = ОпределитьРегистрациюВНалоговомОргане(ТекПодразделениеОтправитель, ДанныеИБ.Организация);

			СтруктураДанныеПодразделения = Новый Структура(
			"Наименование, РегистрацияПодразделенияВНалоговомОргане", 
			Строка(ТекПодразделениеОтправитель),
			РегистрацияПодразделенияВНалоговомОргане);

			Инструкция = Новый Структура(
			"Значение, ИмяПКО",
			СтруктураДанныеПодразделения,
			"Справочник_Подразделения_ИзСтруктуры");

			ДанныеXDTO.Вставить("ПодразделениеОтправитель", Инструкция);

		КонецЕсли;

		Если ЗначениеЗаполнено(ТекПодразделениеПолучатель) Тогда

			РегистрацияПодразделенияВНалоговомОргане = ОпределитьРегистрациюВНалоговомОргане(ТекПодразделениеПолучатель, ДанныеИБ.Организация);

			СтруктураДанныеПодразделения = Новый Структура(
			"Наименование, РегистрацияПодразделенияВНалоговомОргане", 
			Строка(ТекПодразделениеПолучатель),
			РегистрацияПодразделенияВНалоговомОргане);

			Инструкция = Новый Структура(
			"Значение, ИмяПКО",
			СтруктураДанныеПодразделения,
			"Справочник_Подразделения_ИзСтруктуры");

			ДанныеXDTO.Вставить("ПодразделениеПолучатель", Инструкция);

		КонецЕсли;

		Если ДополнитьСоответствие Тогда

			СоответствиеДляПолученияПодразделений.Вставить(СкладОтправитель, ТекПодразделениеОтправитель);
			СоответствиеДляПолученияПодразделений.Вставить(СкладПолучатель, ТекПодразделениеПолучатель);
			КомпонентыОбмена.ПараметрыКонвертации.Вставить("СоответствиеДляПолученияПодразделений", СоответствиеДляПолученияПодразделений);
		КонецЕсли;

	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", ДанныеИБ.Ссылка);

	ДанныеСкладаПолучателя = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДанныеИБ.СкладПолучатель, "ТипСклада,РозничныйВидЦены");

	Если ДанныеСкладаПолучателя.ТипСклада = Перечисления.ТипыСкладов.РозничныйМагазин
		И ЗначениеЗаполнено(ДанныеСкладаПолучателя.РозничныйВидЦены) Тогда

		Запрос.Текст =
		"ВЫБРАТЬ
		|	ВидыЗапасов.АналитикаУчетаНоменклатуры.Номенклатура КАК Номенклатура,
		|	ВидыЗапасов.АналитикаУчетаНоменклатуры.Характеристика КАК Характеристика,
		|	ВидыЗапасов.АналитикаУчетаНоменклатуры.Серия КАК Серия,
		|	ВидыЗапасов.АналитикаУчетаНоменклатуры.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	ВидыЗапасов.Количество КАК Количество,
		|	ВидыЗапасов.НомерГТД КАК НомерГТД,
		|	ВидыЗапасов.КоличествоПоРНПТ КАК КоличествоПоРНПТ,
		|	ВЫБОР
		|		КОГДА ВидыЗапасов.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
		|			ТОГДА ""ВозвратнаяТара""
		|		КОГДА ВидыЗапасов.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
		|			ТОГДА ВЫРАЗИТЬ(""КомиссионныеТовары"" КАК СТРОКА(20))
		|		ИНАЧЕ ВЫРАЗИТЬ(&СобственныеТовары КАК СТРОКА(20))
		|	КОНЕЦ КАК ТипЗапасов,
		|	ВидыЗапасов.ИдентификаторСтроки КАК ИдентификаторСтроки
		|ПОМЕСТИТЬ ТаблицаТоваров
		|ИЗ
		|	Документ.ПеремещениеТоваров.ВидыЗапасов КАК ВидыЗапасов
		|ГДЕ
		|	ВидыЗапасов.Ссылка = &Ссылка
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура,
		|	Характеристика
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаТоваров.Номенклатура КАК Номенклатура,
		|	ТаблицаТоваров.Характеристика КАК Характеристика,
		|	ТаблицаТоваров.Серия КАК Серия,
		|	ТаблицаТоваров.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	ТаблицаТоваров.Количество КАК Количество,
		|	ТаблицаТоваров.ТипЗапасов КАК ТипЗапасов,
		|	ТаблицаТоваров.НомерГТД КАК НомерГТД,
		|	ТаблицаТоваров.КоличествоПоРНПТ КАК КоличествоПоРНПТ,
		|	ЦеныНоменклатурыСрезПоследних.Цена / ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки1, 1) * ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки2, 1) * ВЫБОР
		|		КОГДА &Валюта <> ЦеныНоменклатурыСрезПоследних.Валюта
		|			ТОГДА ВЫБОР
		|					КОГДА ЕСТЬNULL(КурсыВалютыЦены.КурсЗнаменатель, 0) > 0
		|							И ЕСТЬNULL(КурсыВалютыЦены.КурсЧислитель, 0) > 0
		|							И ЕСТЬNULL(КурсыВалюты.КурсЗнаменатель, 0) > 0
		|							И ЕСТЬNULL(КурсыВалюты.КурсЧислитель, 0) > 0
		|						ТОГДА КурсыВалютыЦены.КурсЧислитель * КурсыВалюты.КурсЗнаменатель / (КурсыВалюты.КурсЧислитель * КурсыВалютыЦены.КурсЗнаменатель)
		|					ИНАЧЕ 0
		|				КОНЕЦ
		|		ИНАЧЕ 1
		|	КОНЕЦ КАК ЦенаВРознице,
		|	ТаблицаТоваров.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	&СтавкаНДС КАК СтавкаНДСВРознице
		|ПОМЕСТИТЬ ПервичныеДанные
		|ИЗ
		|	ТаблицаТоваров КАК ТаблицаТоваров
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры.СрезПоследних(
		|				&Дата,
		|				ВидЦены = &ВидЦен
		|					И Номенклатура В
		|						(ВЫБРАТЬ
		|							ТаблицаТоваров.Номенклатура
		|						ИЗ
		|							ТаблицаТоваров КАК ТаблицаТоваров)
		|					И (Характеристика В
		|							(ВЫБРАТЬ
		|								ТаблицаТоваров.Характеристика
		|							ИЗ
		|								ТаблицаТоваров КАК ТаблицаТоваров)
		|						ИЛИ Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка))) КАК ЦеныНоменклатурыСрезПоследних
		|		ПО ТаблицаТоваров.Номенклатура = ЦеныНоменклатурыСрезПоследних.Номенклатура
		|			И ТаблицаТоваров.Характеристика = ЦеныНоменклатурыСрезПоследних.Характеристика
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтносительныеКурсыВалют.СрезПоследних(&Дата, БазоваяВалюта = &БазоваяВалюта) КАК КурсыВалютыЦены
		|		ПО (ЦеныНоменклатурыСрезПоследних.Валюта = КурсыВалютыЦены.Валюта)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтносительныеКурсыВалют.СрезПоследних(&Дата, Валюта = &Валюта И БазоваяВалюта = &БазоваяВалюта) КАК КурсыВалюты
		|		ПО (ИСТИНА),
		|		#ТаблицыСтавкаНДС
		|ГДЕ
		|	(ТаблицаТоваров.ТипЗапасов = &СобственныеТовары
		|			ИЛИ ТаблицаТоваров.ТипЗапасов = ""ВозвратнаяТара"")
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ТаблицаТоваров.Номенклатура,
		|	ТаблицаТоваров.Характеристика,
		|	ТаблицаТоваров.Серия,
		|	ТаблицаТоваров.ЕдиницаИзмерения,
		|	ТаблицаТоваров.Количество,
		|	ТаблицаТоваров.ТипЗапасов,
		|	ТаблицаТоваров.НомерГТД,
		|	ТаблицаТоваров.КоличествоПоРНПТ,
		|	0,
		|	ТаблицаТоваров.ИдентификаторСтроки,
		|	NULL
		|ИЗ
		|	ТаблицаТоваров КАК ТаблицаТоваров
		|ГДЕ
		|	ТаблицаТоваров.ТипЗапасов = ""КомиссионныеТовары""
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПервичныеДанные.Номенклатура КАК Номенклатура,
		|	&ТекстЗапросаХарактеристика,
		|	&ТекстЗапросаСерия,
		|	&ТекстЗапросаРНПТ,
		|	&ТекстЗапросаГТД,
		|	ПервичныеДанные.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	ПервичныеДанные.Количество КАК Количество,
		|	ПервичныеДанные.ТипЗапасов КАК ТипЗапасов,
		|	ВЫБОР
		|		КОГДА ПервичныеДанные.ЦенаВРознице < 0.01
		|			ТОГДА 0.01
		|		ИНАЧЕ ВЫРАЗИТЬ(ПервичныеДанные.ЦенаВРознице КАК ЧИСЛО(31, 2))
		|	КОНЕЦ КАК ЦенаВРознице,
		|	ПервичныеДанные.СтавкаНДСВРознице КАК СтавкаНДСВРознице,
		|	ПервичныеДанные.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	ВЫРАЗИТЬ(ВЫБОР
		|			КОГДА ПервичныеДанные.ЦенаВРознице < 0.01
		|				ТОГДА 0.01
		|			ИНАЧЕ ВЫРАЗИТЬ(ПервичныеДанные.ЦенаВРознице КАК ЧИСЛО(31, 2))
		|		КОНЕЦ * ПервичныеДанные.Количество КАК ЧИСЛО(31, 2)) КАК СуммаВРознице
		|ИЗ
		|	ПервичныеДанные КАК ПервичныеДанные";

		ЗаполнитьСобственныеТоварыВТексте(Запрос.Текст);
		ВерсияФормата17ИВыше = ВерсияФорматаЧислом(КомпонентыОбмена.ВерсияФорматаОбмена) >= ВерсияФорматаЧислом("1.17");
		ЗаполнитьСтавкуНДСВТексте(Запрос.Текст, "ТаблицаТоваров.Номенклатура", ВерсияФормата17ИВыше);
		Запрос.Текст = СтрЗаменить(Запрос.Текст,
		"&ТекстЗапросаКоэффициентУпаковки1",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
		"ЦеныНоменклатурыСрезПоследних.Упаковка",
		"ЦеныНоменклатурыСрезПоследних.Номенклатура"));

		Запрос.Текст = СтрЗаменить(Запрос.Текст,
		"&ТекстЗапросаКоэффициентУпаковки2",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
		"ТаблицаТоваров.ЕдиницаИзмерения",
		"ТаблицаТоваров.Номенклатура"));

		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстЗапросаХарактеристика",
		ПолучитьТекстЗапросаХарактеристики(КомпонентыОбмена, "ПервичныеДанные"));

		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстЗапросаСерия",
		ПолучитьТекстЗапросаСерии(КомпонентыОбмена, "ПервичныеДанные"));

		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстЗапросаРНПТ",
		ПолучитьТекстЗапросаРНПТ_ПеремещениеТоваров(КомпонентыОбмена, "ПервичныеДанные"));

		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстЗапросаГТД",
		ПолучитьТекстЗапросаГТД(КомпонентыОбмена, "ПервичныеДанные"));

		Запрос.УстановитьПараметр("Дата",	КонецДня(ДанныеИБ.Дата));
		Запрос.УстановитьПараметр("Валюта", ЗначениеНастроекПовтИсп.ВалютаРегламентированногоУчетаОрганизации(ДанныеИБ.Организация));
		Запрос.УстановитьПараметр("БазоваяВалюта", ЗначениеНастроекПовтИсп.ВалютаРегламентированногоУчетаОрганизации(ДанныеИБ.Организация));
		Запрос.УстановитьПараметр("ВидЦен", ДанныеСкладаПолучателя.РозничныйВидЦены);
		Запрос.УстановитьПараметр("СтранаРегистрации", ЗначениеНастроекКлиентСерверПовтИсп.СтранаРегистрацииОрганизации(ДанныеИБ.Организация));
		Запрос.УстановитьПараметр("Период", ДанныеИБ.Дата);

	Иначе

		Запрос.Текст =
		"ВЫБРАТЬ
		|	ВидыЗапасов.АналитикаУчетаНоменклатуры.Номенклатура КАК Номенклатура,
		|	ВидыЗапасов.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	&ТекстЗапросаХарактеристика,
		|	&ТекстЗапросаСерия,
		|	&ТекстЗапросаРНПТ,
		|	&ТекстЗапросаГТД,
		|	ВидыЗапасов.АналитикаУчетаНоменклатуры.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	ВидыЗапасов.Количество КАК Количество,
		|	ВЫБОР
		|		КОГДА ВидыЗапасов.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
		|			ТОГДА ""ВозвратнаяТара""
		|		КОГДА ВидыЗапасов.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
		|			ТОГДА ВЫРАЗИТЬ(""КомиссионныеТовары"" КАК СТРОКА(20))
		|		ИНАЧЕ ВЫРАЗИТЬ(&СобственныеТовары КАК СТРОКА(20))
		|	КОНЕЦ КАК ТипЗапасов
		|ИЗ
		|	Документ.ПеремещениеТоваров.ВидыЗапасов КАК ВидыЗапасов
		|ГДЕ
		|	ВидыЗапасов.Ссылка = &Ссылка";

		ЗаполнитьСобственныеТоварыВТексте(Запрос.Текст);
		ВерсияФормата17ИВыше = ВерсияФорматаЧислом(КомпонентыОбмена.ВерсияФорматаОбмена) >= ВерсияФорматаЧислом("1.17");
		ЗаполнитьСтавкуНДСВТексте(Запрос.Текст, "ТаблицаТоваров.Номенклатура", ВерсияФормата17ИВыше);

		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстЗапросаХарактеристика",
		ПолучитьТекстЗапросаХарактеристики(КомпонентыОбмена, "ВидыЗапасов.АналитикаУчетаНоменклатуры"));

		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстЗапросаСерия",
		ПолучитьТекстЗапросаСерии(КомпонентыОбмена, "ВидыЗапасов.АналитикаУчетаНоменклатуры"));

		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстЗапросаРНПТ",
		ПолучитьТекстЗапросаРНПТ_ПеремещениеТоваров(КомпонентыОбмена, "ВидыЗапасов", "ВидыЗапасов.АналитикаУчетаНоменклатуры"));

		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстЗапросаГТД",
		ПолучитьТекстЗапросаГТД(КомпонентыОбмена, "ВидыЗапасов"));

	КонецЕсли;

	Товары = Запрос.Выполнить().Выгрузить();
	ЗаполнитьХарактеристикуУпаковкуВТЧ(КомпонентыОбмена, Товары, Истина, Ложь);
	ЗаполнитьВидДеятельностиНДСВТЧ(КомпонентыОбмена, ДанныеИБ, Товары, "ПеремещениеПодДеятельность", "ПеремещениеПодДеятельность");
	//Если ВерсияФорматаЧислом(КомпонентыОбмена.ВерсияФорматаОбмена) < ВерсияФорматаЧислом("1.17") Тогда   
	//	 ПереопределитьСтавкуНДСДляСтарыхФорматов(Товары, Ложь);
	//КонецЕсли;  
	#Вставка
	// Убираем комплектующие, которых не должно в БП
	МассивСтрокУдаления = Новый Массив;
	Для Каждого ТекСтрока Из ДанныеИБ.Ссылка.Товары Цикл
		Если ТекСтрока.НеВыгружатьВБП Тогда
			СтрОтбора = Новый Структура;
			СтрОтбора.Вставить("Номенклатура", ТекСтрока.Номенклатура);
			НСтроки = Товары.НайтиСтроки(СтрОтбора);
			Для Каждого НСтрока Из НСтроки Цикл
				МассивСтрокУдаления.Добавить(НСтрока);
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	Для Каждого СтрокаУдаления Из МассивСтрокУдаления Цикл
		Товары.Удалить(СтрокаУдаления);
	КонецЦикла;
	#КонецВставки
	ДанныеXDTO.Вставить("Товары", Товары);
	//++ Локализация
	Если ВерсияФорматаЧислом(КомпонентыОбмена.ВерсияФорматаОбмена) >= ВерсияФорматаЧислом("1.16") Тогда
		ИмяТаблицы = "Документ.ПеремещениеТоваров.ВидыЗапасов"; 
		ИдентификаторыИзВидовЗапасов = Истина;
		ПолучитьСведенияОПрослеживаемости(ИмяТаблицы, ДанныеИБ, ДанныеXDTO, КомпонентыОбмена, ИдентификаторыИзВидовЗапасов); 
	КонецЕсли;  
	//-- Локализация
КонецПроцедуры
