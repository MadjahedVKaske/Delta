
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	АсинхВыборПринтера();
		
КонецПроцедуры

&НаКлиенте
Асинх Процедура АсинхВыборПринтера()
	
	СписокПринтеров = Новый СписокЗначений;
	
	Принтеры =  Новый COMОбъект ("WScript.Network");
    prn =  Принтеры.EnumPrinterConnections();
	
	i = 0;
	
	Пока i < prn.Count()-1 Цикл
		
		СписокПринтеров.Добавить(prn.Item(i+1), prn.Item(i+1));
		i = i + 2;
		
	КонецЦикла;
	
	СписокПринтеров.Добавить("Отладка");
	
	
    ОтветАсинх = ВыбратьИзМенюАсинх(СписокПринтеров, Элементы.Принтер);
	
	Ответ = Ждать ОтветАсинх;
	
	Если Ответ = Неопределено Тогда
		Закрыть();		
	ИначеЕсли Ответ = "Отладка" Тогда 
		Принтер = Ответ;
		ПодключитьОбработчикОжидания("ПечатьИзРегистра", 20, Истина);
	Иначе
		Принтер = Ответ;		
		ПодключитьОбработчикОжидания("ПечатьИзРегистра", 5);
	КонецЕсли;
	
   
КонецПроцедуры

&НаКлиенте
Процедура ПечатьИзРегистра() Экспорт
		
	ТабДок = ПолучитьТабличныйДокументДляПечати();
	
	Если ТабДок = Неопределено Тогда
		
	Иначе
		ТабДок.ИмяПринтера = Принтер;
		Если Принтер = "Отладка" Тогда
			ТабДок.Показать();
			ОтключитьОбработчикОжидания("ПечатьИзРегистра");
		Иначе 
			ТабДок.Напечатать();	
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры // ()

&НаСервере
Функция ПолучитьТабличныйДокументДляПечати()

	МассивСН = Новый Массив;
	Если Принтер = "Отладка" Тогда 
		
		СтруктураСерийногоНомера = Новый Структура;
		СтруктураСерийногоНомера.Вставить("СерийныйНомер", "8S-000000001");
		СтруктураСерийногоНомера.Вставить("ИмяМакета", "Макет8S16Ah");
		
		МассивСН.Добавить(СтруктураСерийногоНомера);
		
		СтруктураСерийногоНомера = Новый Структура;
		СтруктураСерийногоНомера.Вставить("СерийныйНомер", "6S-000000001");
		СтруктураСерийногоНомера.Вставить("ИмяМакета", "Макет6S12Ah");
		
		МассивСН.Добавить(СтруктураСерийногоНомера);
		//МассивСН.Добавить("41F8CB5");
	Иначе 
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	СерийныеНомераНаПечать.Период КАК Период,
		|	СерийныеНомераНаПечать.СерийныйНомер КАК СерийныйНомер,
		|	СерийныеНомераНаПечать.Реализация КАК Реализация,
		|	СерийныеНомераНаПечать.Количество КАК Количество,
		|	СерийныеНомераНаПечать.ИмяМакета КАК ИмяМакета,
		|	ИСТИНА КАК Распечатан
		|ИЗ
		|	РегистрСведений.СерийныеНомераНаПечать КАК СерийныеНомераНаПечать
		|ГДЕ
		|	НЕ СерийныеНомераНаПечать.Распечатан
		|	И НЕ СерийныеНомераНаПечать.ДобавленныйСерийныйНомер";
		//|	И СерийныеНомераНаПечать.Реализация ССЫЛКА Документ.РеализацияТоваровУслуг";
		
		Запрос.УстановитьПараметр("Пользователь", ПараметрыСеанса.ТекущийПользователь);
		Результат = Запрос.Выполнить();
		Выборка = Результат.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			ЗаписьРегистра = РегистрыСведений.СерийныеНомераНаПечать.СоздатьМенеджерЗаписи();
			ЗаполнитьЗначенияСвойств(ЗаписьРегистра, Выборка);
			ЗаписьРегистра.Записать();  
			
			СтруктураСерийногоНомера = Новый Структура;
			СтруктураСерийногоНомера.Вставить("СерийныйНомер", Выборка.СерийныйНомер);
			СтруктураСерийногоНомера.Вставить("ИмяМакета", Выборка.ИмяМакета);
			
			МассивСН.Добавить(СтруктураСерийногоНомера);		
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если МассивСН.Количество() Тогда
		
		Копий = 1;
		
		СтруктураДляПечати = Новый Структура;
		СтруктураДляПечати.Вставить("МассивСН", МассивСН);
		СтруктураДляПечати.Вставить("Принтер",Принтер);
		СтруктураДляПечати.Вставить("Копий", Копий);
		
		Об = РеквизитФормыВЗначение("Объект");
		ТабДок = Об.Печать(СтруктураДляПечати);
		
		Возврат ТабДок;
		
	Иначе
		
		Возврат Неопределено;
		
	КонецЕсли;
	

КонецФункции // ()




	
