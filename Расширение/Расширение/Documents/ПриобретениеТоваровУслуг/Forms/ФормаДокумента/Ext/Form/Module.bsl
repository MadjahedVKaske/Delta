
&НаСервере
Процедура Buro1Goz_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	СтраницаГВ_ГОЗ  = Элементы.Добавить("ГОЗ", Тип("ГруппаФормы"), Элементы.ГруппаСтраницы);
	СтраницаГВ_ГОЗ.Заголовок = НСтр("ru = 'Контракты ГОЗ'");
	СтраницаГВ_ГОЗ.Вид = ВидГруппыФормы.Страница;
	
	ПолеДляДокГОЗ = Элементы.Добавить("B1_КонтрактыГОЗ", Тип("ПолеФормы"), СтраницаГВ_ГОЗ);
	ПолеДляДокГОЗ.Вид = ВидПоляФормы.ПолеВвода;                            
	ПолеДляДокГОЗ.Заголовок = "Контракт ГОЗ(документ)";
	ПолеДляДокГОЗ.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Лево;
	ПолеДляДокГОЗ.ПутьКДанным = "Объект.ГозКонтракт";
	
	ПолеДляСебестИТовОрг = Элементы.Добавить("В1_СебестИТовОрг", Тип("ПолеФормы"), СтраницаГВ_ГОЗ);
	ПолеДляСебестИТовОрг.Вид = ВидПоляФормы.ПолеПереключателя;
	ПолеДляСебестИТовОрг.Заголовок = "Не записывать движения по себестоимости и товарам организаций";
	ПолеДляСебестИТовОрг.ПутьКДанным = "Объект.НеЗаписыватьСебестИТовОрг";
	
	ПолеДляСебестИТовОрг = Элементы.Добавить("НеВыгружатьВБП", Тип("ПолеФормы"), СтраницаГВ_ГОЗ);
	ПолеДляСебестИТовОрг.Вид = ВидПоляФормы.ПолеПереключателя;
	ПолеДляСебестИТовОрг.ПутьКДанным = "Объект.НеВыгружатьВБП";
	
	ПолеДляСебестИТовОрг = Элементы.Добавить("СуммаДокументаВРублях", Тип("ПолеФормы"), СтраницаГВ_ГОЗ);
	ПолеДляСебестИТовОрг.Вид = ВидПоляФормы.ПолеПереключателя;
	ПолеДляСебестИТовОрг.ПутьКДанным = "Объект.СуммаДокументаВРублях";
	
	ТЧРасходыГОЗ = Элементы.Добавить("B1_ТЧ_ГОЗ", Тип("ТаблицаФормы"), СтраницаГВ_ГОЗ);
	ТЧРасходыГОЗ.ПутьКДанным = "Объект.ГОЗ";
	ТЧРасходыГОЗ.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	
	Об = РеквизитФормыВЗначение("Объект");
	ТабЧасти = Об.Метаданные().ТабличныеЧасти;	
	Для каждого КолонкаОбъекта Из ТабЧасти.ГОЗ.СтандартныеРеквизиты Цикл
		
		ИмяКолонки = "B1_ТЧ_ГОЗ" + КолонкаОбъекта.Имя;
		
		ЭлементыТаблицыФормы = Элементы.Добавить(ИмяКолонки, Тип("ПолеФормы"), ТЧРасходыГОЗ);
		ЭлементыТаблицыФормы.Вид = ВидПоляФормы.ПолеВвода;
		ЭлементыТаблицыФормы.ПутьКДанным = "Объект.ГОЗ." + КолонкаОбъекта.Имя;	
	
	КонецЦикла;
	
	Для каждого КолонкаОбъекта Из ТабЧасти.ГОЗ.Реквизиты Цикл
		
		ИмяКолонки = "B1_ТЧ_ГОЗ" + КолонкаОбъекта.Имя;
		
		ЭлементыТаблицыФормы = Элементы.Добавить(ИмяКолонки, Тип("ПолеФормы"), ТЧРасходыГОЗ);
		ЭлементыТаблицыФормы.Вид = ВидПоляФормы.ПолеВвода;
		ЭлементыТаблицыФормы.ПутьКДанным = "Объект.ГОЗ." + КолонкаОбъекта.Имя;	
	
	КонецЦикла;
	
	// 
    Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		СпрСсылка = Справочники.ЗакупкаТоваровВРазрезеГозКонтракта.НайтиПоРеквизиту("ПриобретениеТоваровУслуг", Объект.Ссылка);	
		ТоварыПоКонтрактам.Загрузить(СпрСсылка.Товары.Выгрузить());
		ЗатратыПоКонтрактам.Загрузить(СпрСсылка.СтатьиЗатрат.Выгрузить());
	КонецЕсли; 
	
	ПолеПроектРазвития = Элементы.Добавить("ПроектРазвития", Тип("ПолеФормы"), Элементы.ГруппаПараметрыПраво);
	ПолеПроектРазвития.Вид = ВидПоляФормы.ПолеВвода;                            
	ПолеПроектРазвития.ПутьКДанным = "Объект.ПроектРазвития";
     		
КонецПроцедуры

&НаСервере
Процедура Buro1Goz_СохранитьИзмененияПослеНаСервере()
	
	СпрСсылка = Справочники.ЗакупкаТоваровВРазрезеГозКонтракта.НайтиПоРеквизиту("ПриобретениеТоваровУслуг", Объект.Ссылка);
	Если СпрСсылка.Пустая() Тогда
		СпрОб = Справочники.ЗакупкаТоваровВРазрезеГозКонтракта.СоздатьЭлемент();
	Иначе 
		СпрОб = СпрСсылка.ПолучитьОбъект();
	КонецЕсли;
	СпрОб.ПриобретениеТоваровУслуг = Объект.Ссылка; 
	СпрОб.Товары.Загрузить(ТоварыПоКонтрактам.Выгрузить());
	СпрОб.СтатьиЗатрат.Загрузить(ЗатратыПоКонтрактам.Выгрузить());
	СпрОб.Записать();
		
КонецПроцедуры

&НаКлиенте
Процедура Buro1Goz_СохранитьИзмененияПосле(Команда)
	Buro1Goz_СохранитьИзмененияПослеНаСервере();
КонецПроцедуры

&НаСервере
Процедура Buro1Goz_ЗаполнитьПоТоварамПослеНаСервере()
	
	Если Модифицированность Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Запишите документ");
		Возврат;
	КонецЕсли;
	
	ТоварыПоКонтрактам.Загрузить(Объект.Товары.Выгрузить());
	
	Если ЗначениеЗаполнено(Объект.ГозКонтракт) Тогда
		Для Каждого ТекСтрока Из ТоварыПоКонтрактам Цикл
			ТекСтрока.ГозКонтракт = Объект.ГозКонтракт;
		КонецЦикла;
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура Buro1Goz_ЗаполнитьПоТоварамПосле(Команда)
	Buro1Goz_ЗаполнитьПоТоварамПослеНаСервере();
КонецПроцедуры

&НаСервере
Процедура Buro1Goz_ЗаполнитьСтатьиЗатратПоТоварамПослеНаСервере()
	
	ЗатратыПоКонтрактам.Очистить();
	
	ТабРаспределение = ТоварыПоКонтрактам.Выгрузить();
	ТабРаспределение.Свернуть("ГозКонтракт, Номенклатура, Характеристика", "Количество");
	
	ТабТовары = Объект.Товары.Выгрузить();
	ТабТовары.Свернуть("Номенклатура, Характеристика", "СуммаСНДС, Количество");
	
	СтрОтбора = Новый Структура;
	
	Для Каждого ТекСтрока Из ТабРаспределение Цикл
		СтрОтбора.Вставить("Номенклатура", ТекСтрока.Номенклатура);
		СтрОтбора.Вставить("Характеристика", ТекСтрока.Характеристика);
		НСтроки = ТабТовары.НайтиСтроки(СтрОтбора);
		Если НСтроки.Количество() Тогда
			НоваяСтрока = ЗатратыПоКонтрактам.Добавить();
			НоваяСтрока.ГозКонтракт = ТекСтрока.ГозКонтракт;
			НоваяСтрока.Сумма		 = НСтроки[0].СуммаСНДС * ТекСтрока.Количество/НСтроки[0].Количество;
		КонецЕсли;
	КонецЦикла;
	
	ТабСвертки = ЗатратыПоКонтрактам.Выгрузить();
	ТабСвертки.Свернуть("ГозКонтракт, СтатьиРасходовГоз", "Сумма, СуммаДокументаВРублях");
	ЗатратыПоКонтрактам.Загрузить(ТабСвертки);
	  
КонецПроцедуры

&НаКлиенте
Процедура Buro1Goz_ЗаполнитьСтатьиЗатратПоТоварамПосле(Команда)
	Buro1Goz_ЗаполнитьСтатьиЗатратПоТоварамПослеНаСервере();
КонецПроцедуры

&НаСервере
&После("СоглашениеПриИзмененииСервер")
Процедура Buro1Goz_СоглашениеПриИзмененииСервер(ПересчитыватьЦены)
	
	Если ЗначениеЗаполнено(Объект.Соглашение.B1_СтавкаНДС) Тогда
			
		КэшированныеЗначения = ПакетнаяОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруКэшируемыеЗначения();
		ЗаполнитьСтавкуНДСНаСервереПоВсемСтрокам(Объект.Соглашение.B1_СтавкаНДС, КэшированныеЗначения);

		РассчитатьИтоговыеПоказателиПоступления(ЭтотОбъект);
		
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСтавкуНДСНаСервереПоВсемСтрокам(СтавкаНДС, КэшированныеЗначения)

	СтруктураПересчетаСуммы = ПакетнаяОбработкаТабличнойЧастиКлиентСервер.ПараметрыПересчетаСуммыНДСВСтрокеТЧ(Объект);
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", СтруктураПересчетаСуммы);
	СтруктураДействий.Вставить("ПересчитатьСуммуСНДС", СтруктураПересчетаСуммы);
	СтруктураДействий.Вставить("ОчиститьСуммуВзаиморасчетов");
	СтруктураДействий.Вставить("ЗаполнитьДубликатыЗависимыхРеквизитов", ЗависимыеРеквизиты());

	Для каждого СтрокаТабличнойЧасти Из Объект.Товары Цикл
		СтрокаТабличнойЧасти.СтавкаНДС = СтавкаНДС;
		ПакетнаяОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(СтрокаТабличнойЧасти, СтруктураДействий, КэшированныеЗначения);
	КонецЦикла;

	ОбщегоНазначенияУТ.ЗаполнитьДубликатыЗависимыхРеквизитовВКоллекции(Объект.Товары, ЗависимыеРеквизиты());
	РассчитатьИтоговыеПоказателиПоступления(ЭтотОбъект);
	ВзаиморасчетыКлиентСервер.ОбновитьТекстГиперссылкиВалюты(ЭтотОбъект);

	ОбновитьТекстДокументыНаОсновании();

	УстановитьОбязательностьПоставщика();

КонецПроцедуры

&НаКлиенте
&После("ТоварыНоменклатураПриИзмененииВопросПользователюЗавершение")
Процедура Buro1Goz_ТоварыНоменклатураПриИзмененииВопросПользователюЗавершение(Результат, ДополнительныеПараметры)
	
	Если Не Результат Тогда
		Возврат;
	КонецЕсли;
	
	ТекущаяСтрока = ДополнительныеПараметры.ТекущаяСтрока;	
	B1_СтавкаНДС = ОбщегоНазначенияУТВызовСервера.ЗначениеРеквизитаОбъекта(Объект.Соглашение, "B1_СтавкаНДС");
	Если ЗначениеЗаполнено(B1_СтавкаНДС) И НЕ B1_СтавкаНДС = ТекущаяСтрока.СтавкаНДС Тогда
		
		ТекущаяСтрока.СтавкаНДС = B1_СтавкаНДС;
		ТоварыСтавкаНДСПриИзменении(Неопределено);
		
	КонецЕсли;
		
		
КонецПроцедуры
