
&НаСервере
Процедура Buro1Goz_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	
	СтраницаГВ_ГОЗ  = Элементы.Добавить("ГОЗ", Тип("ГруппаФормы"), Элементы.ГруппаСтраницы);
	СтраницаГВ_ГОЗ.Заголовок = НСтр("ru = 'Контракты ГОЗ'");
	СтраницаГВ_ГОЗ.Вид = ВидГруппыФормы.Страница;
	
	ПолеДляДокГОЗ = Элементы.Добавить("B1_КонтрактыГОЗ", Тип("ПолеФормы"), СтраницаГВ_ГОЗ);
	ПолеДляДокГОЗ.Вид = ВидПоляФормы.ПолеВвода;                            
	ПолеДляДокГОЗ.Заголовок = "Контракт ГОЗ(документ)";
	ПолеДляДокГОЗ.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Лево;
	ПолеДляДокГОЗ.ПутьКДанным = "Объект.ГозКонтракт";
	
	ПолеДляДокГОЗ = Элементы.Добавить("B1_ДокументПоступления", Тип("ПолеФормы"), СтраницаГВ_ГОЗ);
	ПолеДляДокГОЗ.Вид = ВидПоляФормы.ПолеВвода;                            
	ПолеДляДокГОЗ.Заголовок = "Партия";
	ПолеДляДокГОЗ.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Лево;
	ПолеДляДокГОЗ.ПутьКДанным = "Объект.ДокументПоступления"; 
	
	ПолеДляСебестИТовОрг = Элементы.Добавить("В1_СебестИТовОрг", Тип("ПолеФормы"), СтраницаГВ_ГОЗ);
	ПолеДляСебестИТовОрг.Вид = ВидПоляФормы.ПолеПереключателя;
	ПолеДляСебестИТовОрг.Заголовок = "Не записывать движения по себестоимости и товарам организаций";
	ПолеДляСебестИТовОрг.ПутьКДанным = "Объект.НеЗаписыватьСебестИТовОрг";
	
	ПолеДляСебестИТовОрг = Элементы.Добавить("ПечатьСерийныхНомеровИзПула", Тип("ПолеФормы"), СтраницаГВ_ГОЗ);
	ПолеДляСебестИТовОрг.Вид = ВидПоляФормы.ПолеПереключателя;
	ПолеДляСебестИТовОрг.ПутьКДанным = "Объект.ПечатьСерийныхНомеровИзПула";   
	
	ПолеДляСебестИТовОрг = Элементы.Добавить("НеВыгружатьВБП", Тип("ПолеФормы"), СтраницаГВ_ГОЗ);
	ПолеДляСебестИТовОрг.Вид = ВидПоляФормы.ПолеПереключателя;
	ПолеДляСебестИТовОрг.ПутьКДанным = "Объект.НеВыгружатьВБП";
	
	ПолеДляДокГОЗ = Элементы.Добавить("НоменклатураСканирования", Тип("ПолеФормы"), Элементы.Группа3);
	ПолеДляДокГОЗ.Вид = ВидПоляФормы.ПолеВвода;                            
	ПолеДляДокГОЗ.Заголовок = "Номенклатура сканирования";
	ПолеДляДокГОЗ.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Лево;
	ПолеДляДокГОЗ.ПутьКДанным = "Объект.НоменклатураСканирования";
	ПолеДляДокГОЗ.КнопкаВыбора = Ложь;
	ПолеДляДокГОЗ.Ширина = 20;
	ПолеДляДокГОЗ.РастягиватьПоГоризонтали = Ложь;
	ПолеДляДокГОЗ.РежимВыбораИзСписка = Истина;
	ПолеДляДокГОЗ.УстановитьДействие(
		"НачалоВыбора", 		   
		"НачалоВыбораНоменклатураСканирования");
	
	Элементы.Переместить(ПолеДляДокГОЗ, Элементы.Группа3, Элементы.ДобавляемыйСерийныйНомер); 
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
	
		ПолеДляДокГОЗ.СписокВыбора.ЗагрузитьЗначения(ПолучитьСписокВыбораНаСервере());	
	
	КонецЕсли;
	
	Buro1Goz_ОбновитьДанныеПослеНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура НачалоВыбораНоменклатураСканирования(Элемент)
	
	МассивВыбора = ПолучитьСписокВыбораНаСервере();
	Элемент.СписокВыбора.ЗагрузитьЗначения(МассивВыбора);
	Если МассивВыбора.Количество() = 0 Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "В табличной части отсутствуют товары, либо не настроен макет для сканируемой номенклатуры!";
		Сообщение.Сообщить();	
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьСписокВыбораНаСервере()

	СписокВыбора = Новый СписокЗначений;
	
	ЗапросНоменклатурыСканирования = Новый Запрос("
			|ВЫБРАТЬ
			|	НастройкаСобственныхСерийныхНомеров.Номенклатура КАК Номенклатура,
			|	НастройкаСобственныхСерийныхНомеров.ИмяМакета КАК ИмяМакета
			|ИЗ
			|	РегистрСведений.НастройкаСобственныхСерийныхНомеров КАК НастройкаСобственныхСерийныхНомеров
			|ГДЕ
			|	НастройкаСобственныхСерийныхНомеров.Номенклатура В (&Номенклатура)");
	
	ЗапросНоменклатурыСканирования.УстановитьПараметр("Номенклатура", Объект.Товары.Выгрузить().ВыгрузитьКолонку("Номенклатура"));
	Возврат ЗапросНоменклатурыСканирования.Выполнить().Выгрузить().ВыгрузитьКолонку("Номенклатура");

КонецФункции

&НаСервере
Процедура Buro1Goz_ОбновитьДанныеПослеНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СерийныеНомераНаПечатьСрезПоследних.СерийныйНомер КАК СерийныйНомер,
	|	1 КАК Количество
	|ИЗ
	|	РегистрСведений.СерийныеНомераНаПечать.СрезПоследних(, ) КАК СерийныеНомераНаПечатьСрезПоследних
	|ГДЕ
	|	СерийныеНомераНаПечатьСрезПоследних.Реализация = &Реализация";
	Запрос.УстановитьПараметр("Реализация", Объект.Ссылка);
	СерийныеНомера.Загрузить(Запрос.Выполнить().Выгрузить());
	ВсегоСерийныхНомеров = СерийныеНомера.Количество();
	
	Элементы.СтраницаСерийныеНомера.Заголовок = СтрШаблон("Серийные номера (%1)", СтрЗаменить(ВсегоСерийныхНомеров, Символы.НПП, ""));
	
КонецПроцедуры

&НаКлиенте
Процедура Buro1Goz_ОбновитьДанныеПосле(Команда)
	Buro1Goz_ОбновитьДанныеПослеНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура Buro1Goz_ДобавитьСерийныйНомерПосле(Команда)
	Buro1Goz_ДобавитьСерийныйНомерПослеНаСервере();
КонецПроцедуры

&НаСервере
Процедура Buro1Goz_ДобавитьСерийныйНомерПослеНаСервере()
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
	
		Сообщить("Предварительно необходимо записать документ!");
		Возврат;	
	
	КонецЕсли;
	
	ПараметрыПоиска = Новый Структура("СерийныйНомер", ДобавляемыйСерийныйНомер);
	НайденнаяСтрока = СерийныеНомера.НайтиСтроки(ПараметрыПоиска);
	
	Если НайденнаяСтрока.Количество() = 0 Тогда 
		
		Если ЗначениеЗаполнено(ДобавляемыйСерийныйНомер) Тогда
			
			ИмяМакета = "";
			
			ЗапросНоменклатурыСканирования = Новый Запрос("
				|ВЫБРАТЬ
				|	НастройкаСобственныхСерийныхНомеров.Номенклатура КАК Номенклатура,
				|	НастройкаСобственныхСерийныхНомеров.ИмяМакета КАК ИмяМакета
				|ИЗ
				|	РегистрСведений.НастройкаСобственныхСерийныхНомеров КАК НастройкаСобственныхСерийныхНомеров
				|ГДЕ
				|	НастройкаСобственныхСерийныхНомеров.Номенклатура В (&Номенклатура)");
			
			Если НЕ ЗначениеЗаполнено(Объект.НоменклатураСканирования) Тогда
							
				ЗапросНоменклатурыСканирования.УстановитьПараметр("Номенклатура", Объект.Товары.Выгрузить().ВыгрузитьКолонку("Номенклатура"));
				РезультатНоменклатурыСканирования = ЗапросНоменклатурыСканирования.Выполнить().Выгрузить();
				Если РезультатНоменклатурыСканирования.Количество() = 1 Тогда
					
					ИмяМакета = РезультатНоменклатурыСканирования[0].ИмяМакета;
					
				Иначе
					
					Сообщить("Не указана номенклатуры сканирования");
					Возврат;	
					
				КонецЕсли;	
				
			Иначе
				
				МассивНом = Новый Массив;
				МассивНом.Добавить(Объект.НоменклатураСканирования);
				
				ЗапросНоменклатурыСканирования.УстановитьПараметр("Номенклатура", МассивНом);
				РезультатНоменклатурыСканирования = ЗапросНоменклатурыСканирования.Выполнить().Выгрузить();
				Если РезультатНоменклатурыСканирования.Количество() = 1 Тогда
					
					ИмяМакета = РезультатНоменклатурыСканирования[0].ИмяМакета; 
					
				Иначе	
					
					Сообщить("Не найдена настройка для номенклатуры сканирования: " + Объект.НоменклатураСканирования);
					Возврат;
					
				КонецЕсли;	
				
			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(ИмяМакета) Тогда
			
				Сообщить("Не установлено имя макета!");
				Возврат;	
			
			КонецЕсли;
			
			МенеджерЗаписи = РегистрыСведений.СерийныеНомераНаПечать.СоздатьМенеджерЗаписи(); 
			
			МенеджерЗаписи.Период = ТекущаяДата();
			МенеджерЗаписи.Реализация = Объект.Ссылка;
			МенеджерЗаписи.СерийныйНомер = ДобавляемыйСерийныйНомер; 
			МенеджерЗаписи.ИмяМакета = ИмяМакета;
			МенеджерЗаписи.ДобавленныйСерийныйНомер = Истина; 
			МенеджерЗаписи.Записать();
			
			Buro1Goz_ОбновитьДанныеПослеНаСервере();
			
		Иначе
			
			Сообщить("Поле серийный номер не заполнено!");
			
		КонецЕсли;		
		
	Иначе 
		
		Сообщить("Данный серийный номер уже добавлен.");
		
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура Buro1Goz_СгенерироватьСерийныеНомераВместо(Команда)
	Buro1Goz_СгенерироватьСерийныеНомераНаСервереВместо();
КонецПроцедуры

&НаСервере
Процедура Buro1Goz_СгенерироватьСерийныеНомераНаСервереВместо()
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
	
		Сообщить("Предварительно необходимо записать документ!");
		Возврат;	
	
	КонецЕсли;
	
	ИмяМакета = "";
	
	ЗапросНоменклатурыСканирования = Новый Запрос("
	|ВЫБРАТЬ
	|   НастройкаСобственныхСерийныхНомеров.Префикс КАК Префикс,
	|   НастройкаСобственныхСерийныхНомеров.ОбщаяДлинаКода КАК ОбщаяДлинаКода,
	|   НастройкаСобственныхСерийныхНомеров.ПоследнийНомер КАК ПоследнийНомер,
	|	НастройкаСобственныхСерийныхНомеров.Номенклатура КАК Номенклатура,
	|	НастройкаСобственныхСерийныхНомеров.ИмяМакета КАК ИмяМакета
	|ИЗ
	|	РегистрСведений.НастройкаСобственныхСерийныхНомеров КАК НастройкаСобственныхСерийныхНомеров
	|ГДЕ
	|	НастройкаСобственныхСерийныхНомеров.Номенклатура В (&Номенклатура)");
	
	Если НЕ ЗначениеЗаполнено(Объект.НоменклатураСканирования) Тогда
		
		ЗапросНоменклатурыСканирования.УстановитьПараметр("Номенклатура", Объект.Товары.Выгрузить().ВыгрузитьКолонку("Номенклатура"));
		РезультатНоменклатурыСканирования = ЗапросНоменклатурыСканирования.Выполнить().Выгрузить();
		Если РезультатНоменклатурыСканирования.Количество() = 1 Тогда
			
			ИмяМакета = РезультатНоменклатурыСканирования[0].ИмяМакета;
			
		Иначе
			
			Сообщить("Не указана номенклатуры сканирования");
			Возврат;	
			
		КонецЕсли;	
		
	Иначе
		
		МассивНом = Новый Массив;
		МассивНом.Добавить(Объект.НоменклатураСканирования);
		
		ЗапросНоменклатурыСканирования.УстановитьПараметр("Номенклатура", МассивНом);
		РезультатНоменклатурыСканирования = ЗапросНоменклатурыСканирования.Выполнить().Выгрузить();
		Если РезультатНоменклатурыСканирования.Количество() = 1 Тогда
			
			ИмяМакета = РезультатНоменклатурыСканирования[0].ИмяМакета; 
			
		Иначе	
			
			Сообщить("Не найдена настройка для номенклатуры сканирования: " + Объект.НоменклатураСканирования);
			Возврат;
			
		КонецЕсли;	
		
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ИмяМакета) Тогда
		
		Сообщить("Не установлено имя макета!");
		Возврат;	
		
	КонецЕсли;
	
	Если КоличествоГенерируемыхНомеров = 0 Тогда
	
		Сообщить("Укажите количество генерируемых серийных номеров!");
		Возврат;	
	
	КонецЕсли;
	
	НачатьТранзакцию();
	
	Попытка
		
		Выборка	= РезультатНоменклатурыСканирования[0];	
		
		БлокировкаДанных = Новый БлокировкаДанных;
		ЭлементБлокировкиДанных = БлокировкаДанных.Добавить("РегистрСведений.НастройкаСобственныхСерийныхНомеров");
		ЭлементБлокировкиДанных.УстановитьЗначение("Номенклатура", Выборка.Номенклатура);
		ЭлементБлокировкиДанных.Режим = РежимБлокировкиДанных.Исключительный;
		БлокировкаДанных.Заблокировать();
						
		ПоследнийНомер = Выборка.ПоследнийНомер;
		
		НоваяЗапись = РегистрыСведений.НастройкаСобственныхСерийныхНомеров.СоздатьМенеджерЗаписи();
		НоваяЗапись.Номенклатура = Выборка.Номенклатура;
		НоваяЗапись.Прочитать();
		НоваяЗапись.ПоследнийНомер = ПоследнийНомер + КоличествоГенерируемыхНомеров;
		НоваяЗапись.Записать();
		
		Для Сч = 1 По КоличествоГенерируемыхНомеров Цикл
			
			ПоследнийНомер = ПоследнийНомер + 1;
			
			ДлинаБезПрефикса = Выборка.ОбщаяДлинаКода - СтрДлина(Выборка.Префикс);
			СформированныйСерийныйНомер = "" + Выборка.Префикс + Формат(ПоследнийНомер, "ЧЦ=" + ДлинаБезПрефикса + "; ЧВН=; ЧГ=0");
			
			ЗаписьПечати = РегистрыСведений.СерийныеНомераНаПечать.СоздатьМенеджерЗаписи();
			
			ЗаписьПечати.Период 		= ТекущаяДата();
			ЗаписьПечати.СерийныйНомер 	= СформированныйСерийныйНомер;
			ЗаписьПечати.Реализация 	= Объект.Ссылка;
			ЗаписьПечати.Количество		= 1;
			ЗаписьПечати.ИмяМакета 		= ИмяМакета;
			
			ЗаписьПечати.Записать();
			
		КонецЦикла; 			
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "" + ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Сообщение.Сообщить();
		
	КонецПопытки;
		
	Buro1Goz_ОбновитьДанныеПослеНаСервере(); 

КонецПроцедуры // ()
