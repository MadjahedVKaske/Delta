
&После("ОбработкаПроведения")
Процедура Buro1Goz_ОбработкаПроведения(Отказ, РежимПроведения) 
	
	Если Не Отказ Тогда
		НаборЗаписей = РегистрыНакопления.УчетШтрихКодов.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Регистратор.Установить(Ссылка);
		Если ПечатьСерийныхНомеровИзПула Тогда
			Запрос = Новый Запрос;
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	СерийныеНомераНаПечать.СерийныйНомер КАК СерийныйНомер
			|ИЗ
			|	РегистрСведений.СерийныеНомераНаПечать КАК СерийныеНомераНаПечать
			|ГДЕ
			|	СерийныеНомераНаПечать.Реализация = &Реализация";
			Запрос.УстановитьПараметр("Реализация", Ссылка);
			РезультатЗапроса = Запрос.Выполнить();
			ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
			Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
				НоваяСтрока = НаборЗаписей.Добавить();
				НоваяСтрока.ВидДвижения = ВидДвиженияНакопления.Расход;
				НоваяСтрока.Количество = 1;
				НоваяСтрока.Период = Дата;
				НоваяСтрока.СерийныйНомер = ВыборкаДетальныеЗаписи.СерийныйНомер;
				НоваяСтрока.Регистратор = Ссылка;
			КонецЦикла;
		КонецЕсли;
		НаборЗаписей.Записать();
	КонецЕсли; 	
		
КонецПроцедуры

&ИзменениеИКонтроль("ПриКопировании")
Процедура Buro1Goz_ПриКопировании(ОбъектКопирования)

	ДокументОснование    = Неопределено;
	ЗаказНаПеремещение   = Неопределено;
	ПеремещениеПоЗаказам = Ложь;
	ВидыЗапасовУказаныВручную = Ложь;
	СостояниеЗаполненияМногооборотнойТары = Перечисления.СостоянияЗаполненияМногооборотнойТары.ПустаяСсылка();

	Для Каждого СтрокаТЧ Из Товары Цикл

		СтрокаТЧ.КодСтроки             = 0;
		СтрокаТЧ.ЗаказНаПеремещение    = Документы.ЗаказНаПеремещение.ПустаяСсылка();
		СтрокаТЧ.Сделка                = Справочники.СделкиСКлиентами.ПустаяСсылка();
		СтрокаТЧ.Назначение            = Неопределено;
		СтрокаТЧ.ИдентификаторСтроки   = "";
		СтрокаТЧ.НазначениеОтправителя = Неопределено;
		#Вставка
		СтрокаТЧ.НеВыгружатьВБП 	   = Ложь;
		#КонецВставки

	КонецЦикла;

	ВидыЗапасов.Очистить();
	Серии.Очистить();

	ИнициализироватьДокумент();

	ИсправлениеДокументов.ПриКопировании(ЭтотОбъект, ОбъектКопирования);

	ОбщегоНазначенияУТ.ОчиститьИдентификаторыДокумента(ЭтотОбъект, "Товары,ВидыЗапасов");

	ПеремещениеТоваровЛокализация.ПриКопировании(ЭтотОбъект, ОбъектКопирования);

	Автор = Пользователи.ТекущийПользователь();

КонецПроцедуры
