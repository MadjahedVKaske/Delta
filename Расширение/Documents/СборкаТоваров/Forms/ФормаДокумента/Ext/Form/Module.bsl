
&НаСервере
Процедура Buro1Goz_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	СтраницаГВ_ГОЗ  = Элементы.Добавить("ГОЗ", Тип("ГруппаФормы"), Элементы.ГруппаСтраницы);
	СтраницаГВ_ГОЗ.Заголовок = НСтр("ru = 'Контракты ГОЗ'");
	СтраницаГВ_ГОЗ.Вид = ВидГруппыФормы.Страница;
	
	ПолеДляДокГОЗ = Элементы.Добавить("B1_КонтрактыГОЗ", Тип("ПолеФормы"), СтраницаГВ_ГОЗ);
	ПолеДляДокГОЗ.Вид = ВидПоляФормы.ПолеВвода;                            
	ПолеДляДокГОЗ.Заголовок = "Контракт ГОЗ(документ)";
	ПолеДляДокГОЗ.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Лево;
	ПолеДляДокГОЗ.ПутьКДанным = "Объект.ГозКонтракт";
	
	ПолеКолВоНормоЧасов = Элементы.Добавить("B1_КолвоНормоЧасов", Тип("ПолеФормы"), СтраницаГВ_ГОЗ);
	ПолеКолВоНормоЧасов.Вид = ВидПоляФормы.ПолеВвода;
	ПолеКолВоНормоЧасов.Заголовок = "Количество нормо-часов на сборку (за ед.)";
	ПолеКолВоНормоЧасов.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Лево;
	ПолеКолВоНормоЧасов.ПутьКДанным = "Объект.КоличествоНормоЧасовНаСборку";
	
	ПолеСтоимостьНормоЧаса = Элементы.Добавить("B1_СтоимостьНормоЧаса", Тип("ПолеФормы"), СтраницаГВ_ГОЗ);
	ПолеСтоимостьНормоЧаса.Вид = ВидПоляФормы.ПолеВвода;
	ПолеСтоимостьНормоЧаса.Заголовок = "Стоимость норма-часа на сборку (руб.)";
	ПолеСтоимостьНормоЧаса.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Лево;
	ПолеСтоимостьНормоЧаса.ПутьКДанным = "Объект.СтоимостьНормоЧасаНаСборку";
	
	ТЧРасходыГОЗ = Элементы.Добавить("B1_ТЧ_ГОЗ", Тип("ТаблицаФормы"), СтраницаГВ_ГОЗ);
	ТЧРасходыГОЗ.ПутьКДанным = "Объект.ГОЗ";
	ТЧРасходыГОЗ.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;    
	
	Об = РеквизитФормыВЗначение("Объект");
	ТабЧасти = Об.Метаданные().ТабличныеЧасти;	
	Для каждого КолонкаОбъекта Из ТабЧасти.ГОЗ.СтандартныеРеквизиты Цикл
		
		ИмяКолонки = "B1_ТЧ_ГОЗ" + КолонкаОбъекта.Имя;
		
		ЭлементыТаблицыФормы = Элементы.Добавить(ИмяКолонки, Тип("ПолеФормы"), ТЧРасходыГОЗ);
		ЭлементыТаблицыФормы.Вид = ВидПоляФормы.ПолеВвода;
		ЭлементыТаблицыФормы.ПутьКДанным = "Объект.ГОЗ." + КолонкаОбъекта.Имя;	
	
	КонецЦикла;
	
	Для каждого КолонкаОбъекта Из ТабЧасти.ГОЗ.Реквизиты Цикл
		
		ИмяКолонки = "B1_ТЧ_ГОЗ" + КолонкаОбъекта.Имя;
		
		ЭлементыТаблицыФормы = Элементы.Добавить(ИмяКолонки, Тип("ПолеФормы"), ТЧРасходыГОЗ);
		ЭлементыТаблицыФормы.Вид = ВидПоляФормы.ПолеВвода;
		ЭлементыТаблицыФормы.ПутьКДанным = "Объект.ГОЗ." + КолонкаОбъекта.Имя;	
	
	КонецЦикла; 
	
	ПолеПроектРазвития = Элементы.Добавить("ПроектРазвития", Тип("ПолеФормы"), Элементы.ГруппаУчет);
	ПолеПроектРазвития.Вид = ВидПоляФормы.ПолеВвода;                            
	ПолеПроектРазвития.ПутьКДанным = "Объект.ПроектРазвития";
    
	Buro1Goz_ОбновитьДанныеПослеНаСервере();
	
КонецПроцедуры 

&НаСервере
Процедура Buro1Goz_ОбновитьДанныеПослеНаСервере()
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	СерийныеНомераНаПечатьСрезПоследних.СерийныйНомер КАК СерийныйНомер,
		|	1 КАК Количество
		|ИЗ
		|	РегистрСведений.СерийныеНомераНаПечать.СрезПоследних(, ) КАК СерийныеНомераНаПечатьСрезПоследних
		|ГДЕ
		|	СерийныеНомераНаПечатьСрезПоследних.Реализация = &Реализация";
		Запрос.УстановитьПараметр("Реализация", Объект.Ссылка);
		СерийныеНомера.Загрузить(Запрос.Выполнить().Выгрузить());
		ВсегоСерийныхНомеров = СерийныеНомера.Количество();
		
	Иначе
		
		ВсегоСерийныхНомеров = 0
		
	КонецЕсли;
	
	Элементы.СтраницаСерийныеНомера.Заголовок = СтрШаблон("Серийные номера (%1)", СтрЗаменить(ВсегоСерийныхНомеров, Символы.НПП, ""));
		
КонецПроцедуры

&НаКлиенте
Процедура Buro1Goz_ОбновитьДанныеПосле(Команда)
	
	Buro1Goz_ОбновитьДанныеПослеНаСервере(); 
	
КонецПроцедуры

&НаСервере
Процедура Buro1Goz_СформироватьНомераВместоНаСервере()
	
	Buro1Goz_ОбновитьДанныеПослеНаСервере();
	
	КоличествоСоздаваемых = Объект.КоличествоУпаковок - ВсегоСерийныхНомеров;
	
	Если КоличествоСоздаваемых > 0 Тогда
				
		НачатьТранзакцию();
		
		Попытка
			
			БлокировкаДанных = Новый БлокировкаДанных;
			ЭлементБлокировкиДанных = БлокировкаДанных.Добавить("РегистрСведений.НастройкаСобственныхСерийныхНомеров");
			ЭлементБлокировкиДанных.УстановитьЗначение("Номенклатура", Объект.Номенклатура);
			ЭлементБлокировкиДанных.Режим = РежимБлокировкиДанных.Исключительный;
			БлокировкаДанных.Заблокировать();
			
			Запрос = Новый Запрос(
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|   НастройкаСобственныхСерийныхНомеров.Префикс КАК Префикс,
			|   НастройкаСобственныхСерийныхНомеров.ОбщаяДлинаКода КАК ОбщаяДлинаКода,
			|   НастройкаСобственныхСерийныхНомеров.ПоследнийНомер КАК ПоследнийНомер,
			|   НастройкаСобственныхСерийныхНомеров.ИмяМакета КАК ИмяМакета
			|ИЗ
			|   РегистрСведений.НастройкаСобственныхСерийныхНомеров КАК НастройкаСобственныхСерийныхНомеров
			|ГДЕ
			|   НастройкаСобственныхСерийныхНомеров.Номенклатура = &Номенклатура");
			Запрос.УстановитьПараметр("Номенклатура", Объект.Номенклатура);
			
			Выборка = Запрос.Выполнить().Выбрать();
			
			ПоследнийНомер = 0;
			
			Если Выборка.Следующий() Тогда
				
				ПоследнийНомер = Выборка.ПоследнийНомер;
								
				НоваяЗапись = РегистрыСведений.НастройкаСобственныхСерийныхНомеров.СоздатьМенеджерЗаписи();
				НоваяЗапись.Номенклатура = Объект.Номенклатура;
				НоваяЗапись.Прочитать();
				НоваяЗапись.ПоследнийНомер = ПоследнийНомер + КоличествоСоздаваемых;
				НоваяЗапись.Записать();
				
				Для Сч = 1 По КоличествоСоздаваемых Цикл
					
					ПоследнийНомер = ПоследнийНомер + 1;
					
					ДлинаБезПрефикса = Выборка.ОбщаяДлинаКода - СтрДлина(Выборка.Префикс);
					СформированныйСерийныйНомер = "" + Выборка.Префикс + Формат(ПоследнийНомер, "ЧЦ=" + ДлинаБезПрефикса + "; ЧВН=; ЧГ=0");
					
					ЗаписьПечати = РегистрыСведений.СерийныеНомераНаПечать.СоздатьМенеджерЗаписи();
					
					ЗаписьПечати.Период 		= ТекущаяДата();
					ЗаписьПечати.СерийныйНомер 	= СформированныйСерийныйНомер;
					ЗаписьПечати.Реализация 	= Объект.Ссылка;
					ЗаписьПечати.Количество		= 1;
					ЗаписьПечати.ИмяМакета 		= Выборка.ИмяМакета;
					
					ЗаписьПечати.Записать();
				
				КонецЦикла; 			
				
				Buro1Goz_ОбновитьДанныеПослеНаСервере();
				
			Иначе
				
				Сообщение = Новый СообщениеПользователю;
				Сообщение.Текст = "Отсутствует настройка создания серийных номеров для " + Объект.Номенклатура;
				Сообщение.Сообщить();
				
			КонецЕсли;
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "" + ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			Сообщение.Сообщить();
			
		КонецПопытки;
				
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура Buro1Goz_СформироватьНомераВместо(Команда)
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
	
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Перед созданием серийных номеров, необходимо записать документ!";
		Сообщение.Сообщить();	
		
		Возврат;
		
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Объект.Номенклатура) ИЛИ НЕ Объект.КоличествоУпаковок Тогда
	
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Укажите номенклатуру и количество!";
		Сообщение.Сообщить();	
		
		Возврат;	
	
	КонецЕсли;
	
	Buro1Goz_СформироватьНомераВместоНаСервере();
	
КонецПроцедуры


